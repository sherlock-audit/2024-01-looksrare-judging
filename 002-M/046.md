Great Carmine Fox

medium

# It's possible to bypass the `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`

## Summary
An edge case allows to bypass the maximum number of deposits allowed per round.

## Vulnerability Detail

Both the [`rolloverETH`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L643) and the [`depositETHIntoMultipleRounds`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312) don't ensure that the amount of deposits in a round is not bigger than [`MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L81). This is generally not a problem because:

1. Both functions only allow to increase the amount of deposits in a round by 1. 
2. If the previous amount of deposits in a round was already equal to the maximum, the round would have been drawn.

However there is an exception to `.2`. The exception lies in the [`_startRound()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L965) function, which does not draw a winner when the protocol is paused:
```solidity
if (
âŒ !paused() &&
    _shouldDrawWinner(numberOfParticipants, round.maximumNumberOfParticipants, round.deposits.length)
) {
    _drawWinner(round, roundId);
} else {
   ...SNIP...
}
```
Because of this, the contracts can end up in a state in which there are more deposits in a single round than allowed. Once the `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND` limit is bypassed it's possible to add a infinite amount of deposits via [`depositETHIntoMultipleRounds`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312) because the winner is only drawn if current amount of deposits is strictly equal to the maximum allowed:
```solidity
 shouldDraw =
   numberOfParticipants == maximumNumberOfParticipants ||
   (numberOfParticipants > 1 && roundDepositCount == MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND);
```

### POC

Here's a runnable POC to copy-paste in `Yolo.depositETHIntoMultipleRounds.t.sol` that proves this is possible. The POC does the following:
1. Starts with an empty round
2. Fill up both the current round and the next one via [`depositETHIntoMultipleRounds`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312)
3. The current round gets drawn
4. Pause the protocol
5. The VRF coordinator calls `fulfillRandomWords()`
6. The next round should be drawn but it's not because the protocol is paused
7. Creates an extra deposit via [`depositETHIntoMultipleRounds`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312), bringing the deposits of the new round to 101.

```solidity
function test_depositETHIntoMultipleRoundsBypassMaxDeposits() public {
    vm.deal(user1, 100 ether);
    vm.deal(user2, 100 ether);
    
    uint256[] memory randomWords = new uint256[](1);
    randomWords[0] = 1;

    uint256[] memory amounts = new uint256[](2);
    amounts[0] = 0.1 ether;
    amounts[1] = 0.1 ether;

    for(uint i; i < 99; i++) {
        vm.prank(user1);
        yolo.depositETHIntoMultipleRounds{value: 0.2 ether}(amounts);
    }

    vm.prank(user2);
    yolo.depositETHIntoMultipleRounds{value: 0.2 ether}(amounts);

    vm.prank(owner);
    yolo.togglePaused();

    vm.prank(VRF_COORDINATOR);
    VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

    vm.prank(owner);
    yolo.togglePaused();

    vm.prank(user1);
    yolo.depositETHIntoMultipleRounds{value: 0.2 ether}(amounts);

    (,,,uint40 cutoffTime,,,,,, IYoloV2.Deposit[] memory deposits) = yolo.getRound(2);

    assertGt(deposits.length, 100);
}
```

## Impact

The worst possible impact is to create an amount of deposits so big that a subsequent call to `fulfillRandomWords()` would consume more than `500000` gas making it revert which according to [chainlink docs](https://docs.chain.link/vrf/v2/security#fulfillrandomwords-must-not-revert) must not happen.

For the sake of clarity about deciding if this issue should be considered valid or not the amount of deposits needed is about 1000. I have a POC about this that I can paste in the comments, not pasting it here because the snippet is long.

## Code Snippet
- [`_startRound()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L965)

## Tool used
Manual Review

## Recommendation
In both [`rolloverETH`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L643) and [`depositETHIntoMultipleRounds`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312) check that the amount of deposits in the current round is less or equal than `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`, as it's done in [`_deposit()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1221-L1223).
