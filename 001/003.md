Sparkly Cloth Moose

high

# Users could deposit 0 ETH into the round

## Summary

Users could deposit 0 ETH into the round via `depositETHIntoMultipleRounds` function, leading to the increase of `round.deposits` and `round.numberOfParticipants`. This situation may result in the round ending prematurely and impact the overall fairness of the game.

## Vulnerability Detail

When depositing ETH into multiple rounds, there is a validation check solely for the total ETH amount, overlooking individual checks for each amount in the `amounts` array. This oversight enables any user to deposit 0 ETH into the rounds, as long as the total amount of ETH is not zero.

An attacker could exploit this to prematurely conclude the round and gain a profit from it.

```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
        //@audit only check the total msg.value is not zero
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }

        uint256 startingRoundId = roundsCount;
        Round storage startingRound = rounds[startingRoundId];
        _validateRoundIsOpen(startingRound);

        _setCutoffTimeIfNotSet(startingRound);

        uint256 expectedValue;
        uint256[] memory entriesCounts = new uint256[](numberOfRounds);

        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);
//@audit amounts[i] could be zero here
            uint256 depositAmount = amounts[i];
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        if (expectedValue != msg.value) {
            revert InvalidValue();
        }

        emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);

        if (
            _shouldDrawWinner(
                startingRound.numberOfParticipants,
                startingRound.maximumNumberOfParticipants,
                startingRound.deposits.length
            )
        ) {
            _drawWinner(startingRound, startingRoundId);
        }
    }
```

Add the test code to `contracts-yolo/test/foundry/Yolo.depositETHIntoMultipleRounds.t.sol` and run it with `forge test --match-test test_depositETHIntoMultipleRounds_With_Zero`.

```diff
diff --git a/contracts-yolo/test/foundry/Yolo.depositETHIntoMultipleRounds.t.sol b/contracts-yolo/test/foundry/Yolo.depositETHIntoMultipleRounds.t.sol
index 338d2c7..a290a38 100644
--- a/contracts-yolo/test/foundry/Yolo.depositETHIntoMultipleRounds.t.sol
+++ b/contracts-yolo/test/foundry/Yolo.depositETHIntoMultipleRounds.t.sol
@@ -494,6 +494,17 @@ contract Yolo_DepositETHIntoMultipleRounds_Test is TestHelpers {
         yolo.depositETHIntoMultipleRounds{value: 10.009 ether}(amounts);
     }

+    function test_depositETHIntoMultipleRounds_With_Zero()
+        public
+        asPrankedUser(user1)
+    {
+        vm.deal(user1, 10 ether);
+        uint256[] memory amounts = _amounts();
+        amounts[0] = 0 ether;
+        amounts[1] = 3 ether;
+        yolo.depositETHIntoMultipleRounds{value: 10 ether}(amounts);
+    }
+
     function test_depositETHIntoMultipleRounds_RevertIf_OnePlayerCannotFillUpTheWholeRound_FirstRound() public {
         vm.deal(user1, 1_000 ether);
         for (uint256 i; i < 99; ++i) {
```

## Impact

Users have the ability to deposit 0 ETH into the round, causing an increase in both `round.deposits` and `round.numberOfParticipants`. This scenario may contribute to the round ending prematurely and potentially affect the overall fairness of the game.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312-L362

## Tool used

Forge 

## Recommendation

It's recommended to check the value of every amount in the `amounts` array.

```diff
diff --git a/contracts-yolo/contracts/YoloV2.sol b/contracts-yolo/contracts/YoloV2.sol
index b8afe96..0591dc1 100644
--- a/contracts-yolo/contracts/YoloV2.sol
+++ b/contracts-yolo/contracts/YoloV2.sol
@@ -335,7 +335,7 @@ contract YoloV2 is
             _incrementUserDepositCount(roundId, round);

             uint256 depositAmount = amounts[i];
-            if (depositAmount % roundValuePerEntry != 0) {
+            if (depositAmount ==0 || depositAmount % roundValuePerEntry != 0) {
                 revert InvalidValue();
             }
             uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
```