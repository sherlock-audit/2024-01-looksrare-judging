Stale Zinc Osprey

medium

# `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND` can be bypassed using `depositETHIntoMultipleRounds` function.

## Summary
`MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND` for upcoming rounds can be bypassed using [depositETHIntoMultipleRounds](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312-L362) function because it only checks for the current round. Hence when the upcoming round is opened it doesn't meet the draw condition and it has to be cancelled manually.

## Vulnerability Detail
Players can deposit ETH into multiple rounds by calling `depositETHIntoMultipleRounds` and bypass the `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND` for upcoming rounds
```javascript
    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }

        [...]

        emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);

353     if (
            _shouldDrawWinner(
                startingRound.numberOfParticipants,
                startingRound.maximumNumberOfParticipants,
                startingRound.deposits.length
            )
        ) {
            _drawWinner(startingRound, startingRoundId);
        }
    }
```
At [L353](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L353), there is a check to draw winner only for current round. And no restriction to the upcoming rounds, so players can deposit to the upcoming rounds unconsciously.
To draw a round, there is a condition
```javascript
    function _shouldDrawWinner(
        uint256 numberOfParticipants,
        uint256 maximumNumberOfParticipants,
        uint256 roundDepositCount
    ) private pure returns (bool shouldDraw) {
        shouldDraw =
            numberOfParticipants == maximumNumberOfParticipants ||
            (numberOfParticipants > 1 && roundDepositCount == MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND);
    }
```
As specified above, if players deposit to the upcoming rounds so that `roundDepositCount` is greater `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`, the above condition will never met and when the mentioned round is open it should be cancelled by manually.

## Impact
Since the depositing by calling `depositETHIntoMultipleRounds` function doesnt' check that maximum number of deposits is reached, players can deposit to upcoming rounds bypassing restriction of maximum number of deposits and it makes the round cannot be drawn when it should be.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare-biginfo2012/blob/afdf67041cc43ba1b96553ac7ed73ccce08a8bba/contracts-yolo/contracts/YoloV2.sol#L312-L362


## Tool used

Manual Review

## Recommendation

Add check to the `depositEth` function like the one in the `_deposit` function.
```diff
    function _depositETH(
        Round storage round,
        uint256 roundId,
        uint256 roundValuePerEntry,
        uint256 depositAmount
    ) private returns (uint256 entriesCount) {
        entriesCount = depositAmount / roundValuePerEntry;
        uint256 roundDepositCount = round.deposits.length;

+       if (roundDepositCount >= MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND) {
+           revert MaximumNumberOfDepositsReached();
+       }
        _validateOnePlayerCannotFillUpTheWholeRound(_unsafeAdd(roundDepositCount, 1), round.numberOfParticipants);

        uint40 currentEntryIndex = _getCurrentEntryIndexWithoutAccrual(round, roundDepositCount, entriesCount);
        // This is equivalent to
        // round.deposits.push(
        //     Deposit({
        //         tokenType: YoloV2__TokenType.ETH,
        //         tokenAddress: address(0),
        //         tokenId: 0,
        //         tokenAmount: msg.value,
        //         depositor: msg.sender,
        //         withdrawn: false,
        //         currentEntryIndex: currentEntryIndex
        //     })
        // );
        // unchecked {
        //     roundDepositCount += 1;
        // }
        uint256 roundDepositsLengthSlot = _getRoundSlot(roundId) + ROUND__DEPOSITS_LENGTH_SLOT_OFFSET;
        uint256 depositDataSlotWithCountOffset = _getDepositDataSlotWithCountOffset(
            roundDepositsLengthSlot,
            roundDepositCount
        );
        // We don't have to write tokenType, tokenAddress, tokenId, and withdrawn because they are 0.
        _writeDepositorAndCurrentEntryIndexToDeposit(depositDataSlotWithCountOffset, currentEntryIndex);
        _writeDepositAmountToDeposit(depositDataSlotWithCountOffset, depositAmount);
        assembly {
            sstore(roundDepositsLengthSlot, add(roundDepositCount, 1))
        }
    }
```

Also, you may modify the `_shouldDrawWinner` function as follows:

```javascript
    function _shouldDrawWinner(
        uint256 numberOfParticipants,
        uint256 maximumNumberOfParticipants,
        uint256 roundDepositCount
    ) private pure returns (bool shouldDraw) {
        shouldDraw =
-           numberOfParticipants == maximumNumberOfParticipants ||
-           (numberOfParticipants > 1 && roundDepositCount == MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND);
+            numberOfParticipants >= maximumNumberOfParticipants ||
+            (numberOfParticipants > 1 && roundDepositCount >= MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND);
    }
```
I think this will solve the issue.