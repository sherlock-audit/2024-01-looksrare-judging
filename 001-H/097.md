Sharp Indigo Ostrich

high

# Anyone can participate in a future round for free

## Summary

Users can call depositETHIntoMultipleRounds() and provide zero amounts for future rounds. This results in the user being added as a participant and depositor into the round without having to pay the protocol any funds for the deposit.

## Vulnerability Detail

depositETHIntoMultipleRounds() allows depositors to deposit into future rounds. To do this, a depositor provides an array of amounts. At no time does the function check that the amount is non-zero. 

To achieve this, a user provides a valid non-zero deposit for at least one round with the equivalent msg.value sent. This bypasses the first initial check in the function.

```solidity
if (msg.value == 0 || numberOfRounds == 0) {
    revert ZeroDeposits();
}
```

From there, the protocol iterates over each round and successfully adds the the user as a depositor via a call to _incrementUserDepositCount() which increase deposit count and participant count for that round:

```solidity
// Contents of _incrementUserDepositCount()
uint256 userDepositCount = depositCount[roundId][msg.sender];
if (userDepositCount == 0) {
    uint256 numberOfParticipants = round.numberOfParticipants;
    if (numberOfParticipants == round.maximumNumberOfParticipants) {
        revert MaximumNumberOfParticipantsReached();
    }
    unchecked {
        // AUDIT: a side effect of allowing zero amounts equal a valid deposit is that the number of participants increase, even though no deposit is made!
        round.numberOfParticipants = uint40(numberOfParticipants + 1);
    }
}
unchecked {
    // AUDIT: a side effect of allowing zero amounts equal a valid deposit is that the deposit count increases, even though no deposit is made!
    depositCount[roundId][msg.sender] = userDepositCount + 1;
}
```

A user can call depositETHIntoMultipleRounds() multiple times as long they are willing to pay for one round of play.

Below is a forge test that shows off a depositor depositing into future rounds for free and borking the system:

- Depositors can fill up all participants for a round by providing a zero amount in that future round. This leads to other participants not being able to participate in that round or future rounds.
- Depositors are considered participants if they provide a zero amount in that future round.

```solidity
function _zeroAmounts() private pure returns (uint256[] memory amounts) {
    amounts = new uint256[](4);
    amounts[0] = 10 ether;
    amounts[1] = 0;
    amounts[2] = 0;
    amounts[3] = 0;
}

function test_AmountZeroMultipleDeposits() public {
    vm.startPrank(owner);
    yolo.updateMaximumNumberOfParticipantsPerRound(2);

    vm.startPrank(user1);
    yolo.depositETHIntoMultipleRounds{value: 10 ether}(_zeroAmounts());

    vm.deal(user2, 10 ether);
    vm.startPrank(user2);
    yolo.depositETHIntoMultipleRounds{value: 10 ether}(_zeroAmounts());

    vm.deal(user3, 10 ether);
    vm.startPrank(user3);
    // AUDIT: revert occurs below since other users have participated in a future round for free and the quota has been reached
    vm.expectRevert();
    yolo.depositETHIntoMultipleRounds{value: 10 ether}(_zeroAmounts());

    for (uint256 i = 2; i <= 4; i++) {
        (
            IYoloV2.RoundStatus status,
            uint40 maximumNumberOfParticipants,
            uint16 protocolFeeBp,
            uint40 cutoffTime,
            uint40 drawnAt,
            uint40 numberOfParticipants,
            address winner,
            uint96 valuePerEntry,
            uint256 protocolFeeOwed,
            IYoloV2.Deposit[] memory deposits
        ) = yolo.getRound(i);

        
        for (uint256 i = 2; i <= 4; i++) {
            // AUDIT: we've had two users deposit zero amounts into future rounds. This should mean that every round has two deposits.
            assertEq(deposits.length, 2);
        }
    }
}
```

## Impact

Users can deposit into future rounds for free. This breaks the entire notion that a round should consist of paying depositors. Malicious users can DOS the depositETHIntoMultipleRounds function filling up as false participants in future rounds. Legitimate depositors will also face unfair rounds where the majority of players deposited nothing, creating low profits when a legitimate depositor wins a round.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol?plain=1#L312-L362

## Tool used

Manual Review

## Recommendation

There should be a check inside depositETHIntoMultipleRounds() which should validate that the amount exceeds at least the value of one round entry. If it is zero, the protocol should either skip the round or revert.

```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
    ...

    for (uint256 i; i < numberOfRounds; ++i) {
        uint256 roundId = _unsafeAdd(startingRoundId, i);
        Round storage round = rounds[roundId];
        uint256 roundValuePerEntry = round.valuePerEntry;

        uint256 depositAmount = amounts[I];
        // AUDIT: check below required
        require(depositAmount > roundValuePerEntry, "Amount must be at least one roundValuePerEntry");
```
