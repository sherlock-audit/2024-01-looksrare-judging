Fit Cedar Perch

high

# Users can win the pot with 0-value entries

## Summary
Users can win the pot with 0-value entries

## Vulnerability Detail
With the `depositETHIntoMultipleRounds` function, a user can deposit in future rounds as well as the current open one. The function allows for a user to create 0-valued deposits for a certain round. 
```solidity
        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }
```
By doing so, the 0-value deposit will have the same `currentEntryIndex` as the previous deposit. 

When `findUpperBound` is used to locate the winning deposit, it works properly if there are no deposits with the same `currentEntryIndex`. However, when the two deposits have the same `currentEntryIndex`, it will return the 2nd. Meaning that if this ticket is drawn, the user with the 0-value deposit will win the whole pot. 
```solidity
    function findUpperBound(uint256[] memory array, uint256 element) internal pure returns (uint256) {
        if (array.length == 0) {
            return 0;
        }

        uint256 low = 0;
        uint256 high = array.length;

        while (low < high) {
            uint256 mid = Math.average(low, high);

            // Note that mid will always be strictly less than high (i.e. it will be a valid array index)
            // because Math.average rounds down (it does integer division with truncation).
            if (array[mid] > element) {
                high = mid;
            } else {
                unchecked {
                    low = mid + 1;
                }
            }
        }

        // At this point `low` is the exclusive upper bound. We will return the inclusive upper bound.
        if (low > 0 && array[low - 1] == element) {
            unchecked {
                return low - 1;
            }
        } else {
            return low;
        }
    }
```
An attacker could back-run all user deposits and get a significant chance of winning the pot at effectively no risk.

## Impact
Loss of funds.

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/libraries/Arrays.sol

## Tool used

Manual Review

## Recommendation
Do not allow 0-value deposits