Howling Cloud Goldfish

high

# `depositETHIntoMultipleRounds` can be abused to create 0 ETH deposit

## Summary

`depositETHIntoMultipleRounds` lacks validation on 0 ETH deposits, attackers can use this feature to mess up the contract storage and force ending a round.

## Vulnerability Detail

In Yolo v2, users can deposit money into a round with three functions: `rolloverETH()` , `deposit` and `depositETHIntoMultipleRounds`. However, the three functions use different validation: `rolloverETH()` and `deposit` ensure that the ETH amount deposited by the user should never be 0:

    function rolloverETH(
        WithdrawalCalldata[] calldata withdrawalCalldata,
        bool payWithLOOKS
    ) external nonReentrant whenNotPaused {
        ...
        if (rolloverAmount < roundValuePerEntry) {
            revert InvalidValue();
        }
        ...
    }

    function _deposit(uint256 roundId, DepositCalldata[] calldata deposits) private {
        ...
        if (msg.value == 0) {
            if (deposits.length == 0) {
                revert ZeroDeposits();
            }
        } else {
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (msg.value % roundValuePerEntry != 0) {    //@Audit blocking 0 ETH deposit
                revert InvalidValue();
        }
        ...
        if (singleDeposit.tokenType == YoloV2__TokenType.ERC721) {
            ...
            uint256 entriesCount = price / round.valuePerEntry;
            if (entriesCount == 0) {    //@Audit blocking 0 ERC721 deposit
                revert InvalidValue();
            }
            ...
        } else if (singleDeposit.tokenType == YoloV2__TokenType.ERC20) {
            ...
            uint256 entriesCount = ((price * amount) / (10 ** IERC20(tokenAddress).decimals())) /round.valuePerEntry;
            if (entriesCount == 0) {    //@Audit blocking 0 ERC20 deposit
                revert InvalidValue();
            }
            ...
        }
    }

However, `depositETHIntoMultipleRounds` function has no such protection:

    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }
        ...
        for (uint256 i; i < numberOfRounds; ++i) {
            ...
            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();    //@Audit notice here
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        if (expectedValue != msg.value) {
            revert InvalidValue();
        }
    ....
    }

As is shown above, `depositETHIntoMultipleRounds` only checks that the user's total deposits are not zero and that each deposit is an integer multiple of roundValuePerEntry. So, an attacker can construct an input sequence containing many zero deposit.

Things can get very tricky around here. when `_depositETH` tries to finish the deposit, the following function logic is executed:

    function _depositETH(
        Round storage round,
        uint256 roundId,
        uint256 roundValuePerEntry,
        uint256 depositAmount
    ) private returns (uint256 entriesCount) {
        entriesCount = depositAmount / roundValuePerEntry;
        uint256 roundDepositCount = round.deposits.length;

        _validateOnePlayerCannotFillUpTheWholeRound(_unsafeAdd(roundDepositCount, 1), round.numberOfParticipants);

        uint40 currentEntryIndex = _getCurrentEntryIndexWithoutAccrual(round, roundDepositCount, entriesCount);
        uint256 roundDepositsLengthSlot = _getRoundSlot(roundId) + ROUND__DEPOSITS_LENGTH_SLOT_OFFSET;

        uint256 depositDataSlotWithCountOffset = _getDepositDataSlotWithCountOffset(
            roundDepositsLengthSlot,
            roundDepositCount
        );
        _writeDepositorAndCurrentEntryIndexToDeposit(depositDataSlotWithCountOffset, currentEntryIndex);
        _writeDepositAmountToDeposit(depositDataSlotWithCountOffset, depositAmount);
        assembly {
            sstore(roundDepositsLengthSlot, add(roundDepositCount, 1))
        }
    }

When `entriesCount` is set to 0, `currentEntryIndex` returns the deposit index of the last user. After this the contract storage is overwritten, making the user to lose his assets forever.

## Impact

1. `depositETHIntoMultipleRounds` can be abused to lock others deposit.
2. attacker can force ending a round by create a lot of 0 ETH deposit.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L732-L734
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1052-L1053
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1104
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1177
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L1417-L1455



## Tool used

Manual Review

## Recommendation

just make sure every single deposit in `depositETHIntoMultipleRounds` cannot be 0.