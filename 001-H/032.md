Stable Chrome Cheetah

high

# `depositETHIntoMultipleRounds()` allow depositing zero amount per round lead to malicious user can win without depositing

## Summary
`depositETHIntoMultipleRounds()` allow depositing zero amount per round lead to malicious user can win without depositing
## Vulnerability Detail
Function `depositETHIntoMultipleRounds()` only check if the transaction's `msg.value` is != 0, but doesn't check if the depositing amount each round is != 0.  
```solidity
    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }


        ...


        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }



            _incrementUserDepositCount(roundId, round);


            uint256 depositAmount = amounts[i]; // <@@ never check if depositAmount != 0
            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;


            entriesCounts[i] = entriesCount;
        }


        if (expectedValue != msg.value) {
            revert InvalidValue();
        }

        ...
    }

```

Because of that, it's possible for user to call to this function with these inputs:
- depositETHIntoMultipleRounds([0,XXX])
- depositETHIntoMultipleRounds([XXX,0])
- depositETHIntoMultipleRounds([XXX,0,0,0,0,0,0,0,0,...,0])
- depositETHIntoMultipleRounds([0,0,0,0,0,0,0,0,0,...,XXX])

Moreover, with zero value depositing, the protocol will still add index to `round.deposits` like normal depositing.

Because of that, malicious user can manipulate the winning percentage of the round, make malicious user can win without depositing anything in this round

Let's have a scenario:(based on real input: https://looksrare.org/yolo/27533)
- In normal case:
    + User A deposit 0.01ETH, B deposit 0.01, C deposit 0.01, D deposit 0.02, E deposit 0.02, F deposit 0.04
    + User A have 1 ticket, B have 1 ticket, C have 1 ticket, D have 2 ticket, E have 2 ticket, F have 4 ticket
    + The entryIndex array will look like this: [1,2,3,5,7,11]

    + `fulfillRandomWords()` will random a number from 1 -> 11, and then choose winning index in `round.deposits` array by `[1,2,3,5,7,11].findUpperBound(WINNING_ENTRY)`

|          | A     | B     | C     | D      | E      | F      |
|----------|-------|-------|-------|--------|--------|--------|
| Deposit  | 1     | 1     | 1     | 2      | 2      | 4      |
| Ticket   | 1     | 1     | 1     | 2      | 2      | 4      |
| Win rate | 9.09% | 9.09% | 9.09% | 18.18% | 18.18% | 36,36% |


| WINNING_ENTRY                                | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 10 | 11 |
|----------------------------------------------|---|---|---|---|---|---|---|---|---|----|----|
| [1,2,3,5,7,11].findUpperBound(WINNING_ENTRY) | 0 | 1 | 2 | 3 | 3 | 4 | 4 | 5 | 5 | 5  | 5  |
| winner                                       | A | B | C | D | D | E | E | F | F | F  | F  |

- In malicious case:
    + right after normal users deposit, malicious user `X` will send a zero value depositing:  `depositETHIntoMultipleRounds([0,...,XXX])`
    + now the entryIndex array will look like this: [1,1,2,2,3,3,5,5,7,7,11,11].
    + `fulfillRandomWords()` still random a number from 1 -> 11
    + as you can see in the table below, X's win rate is 6/11 ~ 54% without depositing any money in this round

| WINNING_ENTRY                                             | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8  | 9  | 10 | 11 |
|-----------------------------------------------------------|---|---|---|---|---|---|---|----|----|----|----|
| [1,1,2,2,3,3,5,5,7,7,11,11].findUpperBound(WINNING_ENTRY) | 1 | 3 | 5 | 6 | 7 | 8 | 9 | 10 | 10 | 10 | 11 |
| Winner                                                    | X | X | X | D | X | E | X | F  | F  | F  | X  |


Note that this is not the most extreme case. In extreme case (normal user only deposit 0.01ETH each), malicious user's win rate is 100%!

## Impact
As I describe above, the main impact is that malicious user can increase chances of winning, up to 100%.

Another impact is that malicious users can clog future rounds with zero value. Malicious user can deposit 100 zero-value-depositing in the future round using 2 accounts. Because 

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312C1-L362C6
## Tool used

Manual Review

## Recommendation
```diff
    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        uint256 numberOfRounds = amounts.length;
        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }

        uint256 startingRoundId = roundsCount;
        Round storage startingRound = rounds[startingRoundId];
        _validateRoundIsOpen(startingRound);

        _setCutoffTimeIfNotSet(startingRound);

        uint256 expectedValue;
        uint256[] memory entriesCounts = new uint256[](numberOfRounds);

        for (uint256 i; i < numberOfRounds; ++i) {
            uint256 roundId = _unsafeAdd(startingRoundId, i);
            Round storage round = rounds[roundId];
            uint256 roundValuePerEntry = round.valuePerEntry;
            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];
-           if (depositAmount % roundValuePerEntry != 0) {
+           if (depositAmount % roundValuePerEntry != 0 || depositAmount / roundValuePerEntry == 0) {
                revert InvalidValue();
            }
            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        if (expectedValue != msg.value) {
            revert InvalidValue();
        }

        emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);

        if (
            _shouldDrawWinner(
                startingRound.numberOfParticipants,
                startingRound.maximumNumberOfParticipants,
                startingRound.deposits.length
            )
        ) {
            _drawWinner(startingRound, startingRoundId);
        }
    }
```