Abundant Vanilla Cobra

medium

# The whole protocol can become temporary DDOS'ed for the whole `roundDuration`

## Summary

A new round can become bricked for the whole `roundDuration`, if the protocol has been paused in case of an emergency and the current `roundId` has been cancelled.

## Vulnerability Detail

**STATE**:
`roundsCount = 1` - **OUR CURRENT ROUND(1)**

Users can deposit for future(multiple) rounds by `depositETHIntoMultipleRounds`:


```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {

        uint256 numberOfRounds = amounts.length;

        if (msg.value == 0 || numberOfRounds == 0) {
            revert ZeroDeposits();
        }

        uint256 startingRoundId = roundsCount;

        Round storage startingRound = rounds[startingRoundId];

        _validateRoundIsOpen(startingRound);

        _setCutoffTimeIfNotSet(startingRound);

        uint256 expectedValue;

        uint256[] memory entriesCounts = new uint256[](numberOfRounds);
 
        for (uint256 i; i < numberOfRounds; ++i) {

            uint256 roundId = _unsafeAdd(startingRoundId, i);

            Round storage round = rounds[roundId];

            uint256 roundValuePerEntry = round.valuePerEntry;

            if (roundValuePerEntry == 0) {
                (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
            }

            _incrementUserDepositCount(roundId, round);

            uint256 depositAmount = amounts[i];

            if (depositAmount % roundValuePerEntry != 0) {
                revert InvalidValue();
            }

            uint256 entriesCount = _depositETH(round, roundId, roundValuePerEntry, depositAmount);

            expectedValue += depositAmount;

            entriesCounts[i] = entriesCount;
        }

        if (expectedValue != msg.value) {
            revert InvalidValue();
        }

        emit MultipleRoundsDeposited(msg.sender, startingRoundId, amounts, entriesCounts);

        if (
            _shouldDrawWinner(
                startingRound.numberOfParticipants,
                startingRound.maximumNumberOfParticipants,
                startingRound.deposits.length
            )
        ) {
            _drawWinner(startingRound, startingRoundId);
        }
        
    }

```

Let's imagine that a group of depositors want to fill up the **next round(2)** by either maxing out the deposits or the people participating in the round (that's needed for a round to be drawn). This is possible by the function above - to fill up the next round even if the current round is still outgoing and not ready to be drawn(finished).

`roundsCount = 1`, this is our **current**  round, now imagine an emergency occurs and the contract is paused, then the owner decides to cancel the current round (1):

```solidity
function cancel(uint256 numberOfRounds) external {
        _validateIsOwner();

        if (numberOfRounds == 0) {
            revert ZeroRounds();
        }

        uint256 startingRoundId = roundsCount;

        for (uint256 i; i < numberOfRounds; ++i) {

            uint256 roundId = _unsafeAdd(startingRoundId, i);

            rounds[roundId].status = RoundStatus.Cancelled;
            
        }

        emit RoundsCancelled(startingRoundId, numberOfRounds);
        _startRound({_roundsCount: _unsafeSubtract(_unsafeAdd(startingRoundId, numberOfRounds), 1)});
    }

```

Owner wants to cancel **only** the current round(1), so `_startRound` is called at the end of the function, which will start the next new round(2) - which is already filled up and ready to be withdrawn:

```solidity
function _startRound(uint256 _roundsCount) private returns (uint256 roundId) {

        unchecked {
            roundId = _roundsCount + 1;
        }

        roundsCount = uint40(roundId);

        Round storage round = rounds[roundId];
        
        //if there is existing information for this current round, go in
        if (round.valuePerEntry == 0) {
            
            //create the additional info in order to get this round going
            // On top of the 4 values covered by _writeDataToRound, this also writes the round's status to Open (1).
            _writeDataToRound({roundId: roundId, roundValue: 1});

            emit RoundStatusUpdated(roundId, RoundStatus.Open);

        } else {

            uint256 numberOfParticipants = round.numberOfParticipants;

            if (
                !paused() &&
                _shouldDrawWinner(numberOfParticipants, round.maximumNumberOfParticipants, round.deposits.length)
            ) {

                _drawWinner(round, roundId);

            }
            
            
             else {

                uint40 _roundDuration = roundDuration;

                // This is equivalent to
                // round.status = RoundStatus.Open;
                // if (round.numberOfParticipants > 0) {
                //   round.cutoffTime = uint40(block.timestamp) + _roundDuration;
                // }
                uint256 roundSlot = _getRoundSlot(roundId);

                assembly {

                    // RoundStatus.Open is equal to 1.
                    let roundValue := or(sload(roundSlot), 1)

                    if gt(numberOfParticipants, 0) {
                        roundValue := or(roundValue, shl(ROUND__CUTOFF_TIME_OFFSET, add(timestamp(), _roundDuration)))
                    }

                    sstore(roundSlot, roundValue)
                }

                emit RoundStatusUpdated(roundId, RoundStatus.Open);
            }
        }
    }

```

Now here is the tricky part. The first **if** will be skipped since when we interacted with `depositETHIntoMultipleRounds` it already set the `round.valuePerEntry` and other important states for the round. The second if will be skipped too because the round is **paused**, and we will go straight into the **else** which will set our **round.cutoffTime = uint40(block.timestamp) + _roundDuration** and set the round to **OPEN**.

Now what happened here, we have a ready to be withdrawn `round`, **however** it is still with an active `round.cutoffTime` and it is open.

Emergency has been successfully mitigated and owner unpauses the contract. Now you would think that we can directly call `drawWinner` since the round is ready to be withdrawn, but that's not the case since `drawWinner` requires:

```solidity
if (block.timestamp < round.cutoffTime) {
            revert CutoffTimeNotReached();
        }
```

Which in our case is **true**-  block.timestamp is smaller than the cutoffTime. So we can't start a new round since the `roundDuration` must pass in order to pass the **if** statement.

There is another way to start a new round and that is by using any of the `deposit` function, but they will revert too, since they require that the `roundDeposits != MaxRoundDeposits` which in our case is not true since the older depositors have already filled up that round, so depositing can't draw the round either.

Basically now the protocol is frozen for the whole `roundDuration`, no actions can be made until it expires and the round can be withdrawn(finalized) and moved onto the next.

## Impact

Temporary DDOS of the contract's functionality.

## Code Snippet

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L312-L362

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L401-L416

https://github.com/sherlock-audit/2024-01-looksrare/blob/main/contracts-yolo/contracts/YoloV2.sol#L949-L992

## Tool used

Manual Review

## Recommendation

I am not sure of any robust mitigation for this one
