Perfect Pickle Starling

medium

# Users can avoid pay protocol fees when paying with ETH in the claimPrizes().

## Summary
**`claimPrizes()`** serves as the mechanism for winners to redeem their prize. Users have the option to cover the associated fees using ETH. However, due to an implementation error, users may inadvertently pay no fees to the protocol or incur reduced fees.
## Vulnerability Detail
The [**`claimPrizes()`**](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L473C5-L544C6) is utilized by the winner to collect their reward. The issue lies in line 524, where the subtraction of **`msg.value`** from **`protocolFeeOwed`** occurs. If this result is lower than **`ethAmount`**, the fees are deducted from **`ethAmount`**, which represents the winner's reward. In essence, users end up covering the fees by subtracting them directly from the earned reward.
```Solidity
protocolFeeOwed -= msg.value;
if (protocolFeeOwed <= ethAmount) {
   unchecked {
           ethAmount -= protocolFeeOwed;
    }
} else {
     revert ProtocolFeeNotPaid();
}
```
The issue arises from the sequence where **`protocolFeeOwed`** is reduced by **`msg.value`**, followed by subtracting **`protocolFeeOwed`** from **`ethAmount`**. This allows users to circumvent paying fees to the protocol by sending an exact **`msg.value`**. To illustrate this point more clearly, let me provide an example.

- ethAmount = 3 ether
- msg.value = 0.5 ether
- protocolFeeOwed = 0.5 ether

1. Caluclates the protocolFeeOwed:
**`protocolFeeOwed -= msg.value -> protocolFeeOwed = 0.5 - 0.5 = 0`**
2. Subtracting for the Winner's Reward:
**`ethAmount -= protocolFeeOwed -> ethAmount = 3 - 0 = 3`**

As evident, the winner incurs no fees when submitting the exact fee amount, paying 0 in this scenario. However, if the user submits an amount less than the exact fee, such as msg.value = 0.25, they will only pay a 0.25 fee. Basically, the fees are shifted onto other users (balance of the contract).

Finally the **`ethAmount`** is sended to the winner:
```Solidity
 if (ethAmount != 0) {
            _transferETHAndWrapIfFailWithGasLimit(WETH, msg.sender, ethAmount, gasleft());
        }
```
## POC
To execute the POC, copy the following code into **`Yolo.claimPrizes.t.sol`**.
```Solidity
function test_claimPrizes_custom() public {
        _playMultipleRounds();

        address winner = _getWinner(1);
        //balance for pay the tx fee
        assertEq(winner.balance, 0.018000000000000006 ether);

        //Provide the user with the exact fee amount they need to pay to the protocol
        vm.deal(winner, 0.4428 ether);
        deal(LOOKS, winner, 0);
        deal(USDC, winner, 0);

        vm.startPrank(winner);

        //create the prizesIndices array
        uint256[] memory prizesIndices = new uint256[](2);
        prizesIndices[0] = 0;
        prizesIndices[1] = 1;
        IYoloV2.WithdrawalCalldata[] memory withdrawalCalldata = new IYoloV2.WithdrawalCalldata[](2);
        withdrawalCalldata[0].roundId = 1;
        withdrawalCalldata[0].depositIndices = prizesIndices;
        withdrawalCalldata[1].roundId = 2;
        withdrawalCalldata[1].depositIndices = prizesIndices;

        expectEmitCheckAll();
        emit ProtocolFeePayment(0.4428 ether, address(0));

        expectEmitCheckAll();
        emit PrizesClaimed(winner, withdrawalCalldata);

        //claim the prize sending the exact amount of ether to pay the fees
        yolo.claimPrizes{value: 0.4428 ether}(withdrawalCalldata, false);


        //prize = 2.5572 fee = 0.4428 -> UserFinalBalance = 2.5572 + 0.4428 = 3 (fee paid = 0)
        assertEq(winner.balance, 3 ether);
        assertEq(protocolFeeRecipient.balance, 0.4428 ether);
        assertEq(address(yolo).balance, 0);

        vm.stopPrank();
    }
```
## Impact
Users can circumvent paying fees, resulting in the burden of those fees being shifted onto other users.
## Code Snippet
https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L473C5-L544C6
## Tool used
Manual Review.
## Recommendation
Store the variable for comparison in another variable.
```diff
-               protocolFeeOwed -= msg.value;
+               uint256 protocolFeeOwedTemporal =  protocolFeeOwed - msg.value;
+               if (protocolFeeOwedTemporal <= ethAmount) {
                    unchecked {
                        ethAmount -= protocolFeeOwed;
                    }
                } else {
                    revert ProtocolFeeNotPaid();
                }
```